// ****************************************************************************
//  flares.ddd                                                      Tao project
// ****************************************************************************
//
//   File Description:
//
//     Example of use case of flares module.
//
//
//
//
//
//
//
//
// ****************************************************************************
// This document is released under the GNU General Public License.
// See http://www.gnu.org/copyleft/gpl.html and Matthew 25:22 for details
//  (C) 1992-2010 Christophe de Dinechin <christophe@taodyne.com>
//  (C) 2010 Jérôme Forissier <jerome@taodyne.com>
//  (C) 2010 Catherine Burvelle <cathy@taodyne.com>
//  (C) 2010 Taodyne SAS
// ****************************************************************************


import Flares 


// ============================================================================
//
//                                   PAGES
//
// ============================================================================


page "Flares",
// -------------------------------------------------
//    Main scene
// -------------------------------------------------
    color "white" 
    locally 
        flare stime, saturation
        fullscreen_quad 
    locally 
        color "white"
        text_box 0, 0, window_width * 75% , window_height, 
            font "Orbitron", 56 * (window_width / 1200)
            vertical_align_center 
            align_center 
            description_text tao_language 


// ============================================================================
//
//                                   HELP
//
// ============================================================================


description_text "fr" -> 
// -------------------------------------------------
//    French help
// -------------------------------------------------
    text "Déplacez la souris :"
    paragraph_break
    text " * Horizontalement pour changer la couleur"
    paragraph_break
    text " * Verticalement pour régler la vitesse"

description_text T:text -> 
// -------------------------------------------------
//    English help
// -------------------------------------------------
    text "Move the mouse:"
    paragraph_break
    text " * Left/right to change color saturation"
    paragraph_break
    text " * Up/down to change animation speed"


// ============================================================================
//
//                                 FUNCTIONS
//
// ============================================================================


// Fullscreen display
fullscreen_quad -> rectangle 0, 0, window_width, window_height 

// Define a specific time for flares effect
stime ->
    cur_time -> 0.0
    prev -> 0.0 
    cur_time := cur_time + (page_time - prev) * 750 * (0.5 + mouse_y / window_height)
    prev := page_time
    cur_time

// Define saturation for flares effect
saturation -> 2.0 * (0.5 + mouse_x / window_width)
