// ****************************************************************************
//  opening_pane.ddd                                                Tao project
// ****************************************************************************
//
//   File Description:
//
//     Template opening_pane.
//     The background used for this template is in theme.xl file
//
//
//
//
//
//
//
// ****************************************************************************
// This document is released under the GNU General Public License.
// See http://www.gnu.org/copyleft/gpl.html and Matthew 25:22 for details
//  (C) 2012 Christophe de Dinechin <christophe@taodyne.com>
//  (C) 2012 Catherine Burvelle <cathy@taodyne.com>
//  (C) 2012 Taodyne SAS
// ****************************************************************************


import Animate 1.0

// ============================================================================
//
//                                   PAGES
// 
// ============================================================================

clear_color 0.416, 0.671, 0.765, 1 
page "init",
    scale_contents
    text_box 0,0,400,400,
        color watermark
        paragraph_space 30, 30
        align_center
        text "Page de démarrage" en "Initial page."
        paragraph_break
        text_span
            bold
            text "Appuyez sur la touche 'flèche bas' pour voir l'exemple." en <<Hit "down" key to see the sample>>

        paragraph_break

        text "Revenez à cette page pour redémarrer." en "Come back to this page to watch the show again."


page "opening pane",
// -------------------------------------------------
//    Main scene
// -------------------------------------------------
    background -1000
    scale_contents
    op_story 1, 8, 0, -300, -100, 250, 150,
        op_text "Mon text peut être " en "My text can be "
        op_keyword "affiché" en "shown"
        op_text " et " en " and "
        op_keyword "caché" en "hidden"
        op_text "comme je veux." en " as I want."

    op_story 4, 10, 35, 350, 350, 500, 50,
        align_center
        op_text "Une longue ligne de texte..." en "A specially long line of text..."

    op_story 7, 12, 100, -520, 250, 290, 350,
        align_center
        image 0, 0, 1.5, 1.5, "papillon.png"

    op_story 10, 16, 20 * page_time, 200, -250, 249, 250,
        paragraph_space 15, 15
        font "Baskerville", 48
        margins 60, 0
        op_bullet 10, "Élément 1" en "Bullet #1"
        op_bullet 11, "Élément 2" en "Bullet #2"
        op_bullet 12, "Élément 3" en "Bullet #3"
        op_bullet 13, "Élément 4" en "Bullet #4"


// ============================================================================
//
//                                 CONFIGURATION
//
// ============================================================================


font "op_text" -> font "Baskerville", 58
font "op_keyword" -> font "Arial Rounded MT Bold", 58
color "story" -> color "white"
line_color "story"-> line_color "white"

// Logical dimensions for the slide. You can put anything you want here,
// but the aspect ratio should match that of the background picture and be
// close to the aspect ratio of the screens you are going to use.
// This will be used to define the logical coordinates of anything that 
// follows "scale_contents".
slide_width := 1920.0
slide_height := 1200.0

// ============================================================================
//
//                                 FUNCTIONS
//
// ============================================================================


op_story Start:real,End:real,Hue:real,X:real,Y:real,W:real,H:real,Body ->
// ----------------------------------------------------------------------------
//   Draw a story box
// ----------------------------------------------------------------------------
// The box is shown from "Start" seconds after the page is open,
// and until "End" seconds.
// The background color has the given "Hue".
// The box is located in "X", "Y" with size "W"x"H"
// The box displays the "Body"
    if page_time >= Start-1 and page_time <= End+1 then
        locally
            SW->1.0
            SH->1.0

            if W > H then
                SW := fade_between(page_time, Start, End)
                SH := 1.0
            else
                SW := 1.0
                SH := fade_between(page_time, Start, End)

            color_hsv Hue, 0.8, 0.4, 0.5
            rounded_rectangle X, Y, W*SW+20, H*SH+20, 20

            color "story"
            frame_texture 2*W*SW, 2*H*SH,
                text_box 0, 0, 2*W, 2*H,
                    align 0
                    vertical_align 0.5
                    Body
            rectangle X, Y, W*SW, H*SH


op_bullet Time:real, T:text ->
// ----------------------------------------------------------------------------
//   Draw a given bullet
// ----------------------------------------------------------------------------
// The text "T" appears after "Time" seconds
    paragraph_break
    show fade_at(page_time, Time)
    font "op_text"
    anchor
        translate -20, 12, 0
        rotatey 150 * page_time
        color "story", 0.6
        line_color "story"
        line_width 1.5
        circle 0, 0, 9
    text T


op_keyword T:text ->
// ----------------------------------------------------------------------------
//   Display the keywords for a given box
// ----------------------------------------------------------------------------
    font "op_keyword"
    text T


op_text T:text ->
// ----------------------------------------------------------------------------
//   Display text in a glass box
// ----------------------------------------------------------------------------
    font "op_text"
    text T



// ============================================================================
// 
//   Language management
// 
// ============================================================================

lang -> "fr"
Fr:text en En:text -> if lang = "en" then En else Fr
En:text fr Fr:text -> if lang = "fr" then Fr else En
french Body -> if lang = "fr" then Body 
english Body -> if lang = "en" then Body 
language L:text -> lang := L; refresh 0.1

// ============================================================================
// 
//    Keys for the presentations in this directory
// 
// ============================================================================

presentation_eye_distance -> 30
key "e" -> language "en"
key "f" -> language "fr"
