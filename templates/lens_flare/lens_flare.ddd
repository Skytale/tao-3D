// ****************************************************************************
//  lens_flare.ddd                                                  Tao project
// ****************************************************************************
//
//   File Description:
//
//    Example of lens flare with Tao Presentations.
//
//
//
//
//
//
//
//
// ****************************************************************************
// Copyright Taodyne SAS, 2010
// Contributors:
//   Christophe de Dinechin <christophe@taodyne.com>
//   Jerome Forissier <jerome@taodyne.com>
//   Baptiste Soulisse <baptiste.soulisse@taodyne.com>
//
// <contact@taodyne.com>
//
// This software is a computer program whose purpose is to serve as a
// document template for the Tao Presentations application.
//
// This software is governed by the CeCILL license under French law and
// abiding by the rules of distribution of free software.  You can  use,
// modify and/ or redistribute the software under the terms of the CeCILL
// license as circulated by CEA, CNRS and INRIA at the following URL
// "http://www.cecill.info".
//
// As a counterpart to the access to the source code and  rights to copy,
// modify and redistribute granted by the license, users are provided only
// with a limited warranty  and the software's author,  the holder of the
// economic rights,  and the successive licensors  have only  limited
// liability.
//
// In this respect, the user's attention is drawn to the risks associated
// with loading,  using,  modifying and/or developing or reproducing the
// software by the user in light of its specific status of free software,
// that may mean  that it is complicated to manipulate,  and  that  also
// therefore means  that it is reserved for developers  and  experienced
// professionals having in-depth computer knowledge. Users are therefore
// encouraged to load and test the software's suitability as regards their
// requirements in conditions enabling the security of their systems and/or
// data to be ensured and,  more generally, to use and operate it in the
// same conditions as regards security.
//
// The fact that you are presently reading this means that you have had
// knowledge of the CeCILL license and that you accept its terms.
// ****************************************************************************


import LensFlare


// Background color
clear_color 0, 0, 0, 0

// ============================================================================
//
//                                   PAGES
//
// ============================================================================


page "lens flare",
// -------------------------------------------------
//    Main scene
// -------------------------------------------------
    d -> window_height
    locally
        milkyway 8000
        sun 100, 300, -300 + 200 * abs (sin (page_time * 0.04))
        moon d * 20%
        earth d * 60%
    translatey 500 * abs (sin (page_time * 0.04))
    sun_flare 100, 0, -1000, -150, -500 * abs (sin (page_time * 0.04)), 2000


// ============================================================================
//
//                                 FUNCTIONS
//
// ============================================================================


sun X, Y, Z ->
// -------------------------------------------------
//    Draw sun as a light
// -------------------------------------------------
    light 0
    light_ambient 0.0, 0.0, 0.0, 1.0
    light_diffuse 1.0, 1.0, 1.0, 1.0
    light_position X, Y, Z

earth R ->
// -------------------------------------------------
//    Draw earth
// -------------------------------------------------
    locally
        material_ambient 0.4, 0.4, 0.4, 1.0
        material_diffuse 1.0, 1.0, 1.0, 1.0
        texture "earth.bmp"
        texture_wrap true, true
        rotatey 5 * page_time + 10
        sphere R

moon R ->
// -------------------------------------------------
//    Draw moon
// -------------------------------------------------
    locally
        material_ambient 0.3, 0.3, 0.2, 1.0
        material_diffuse 1.0, 1.0, 0.8, 1.0
        texture "moon.jpg"
        texture_wrap true, true
        rotatez 75
        rotatey 15 * page_time + 10
        translatex 1000
        sphere R

milkyway R ->
// -------------------------------------------------
//    Draw milkyway
// -------------------------------------------------
    locally
        material_ambient 0.3, 0.3, 0.3, 1
        material_diffuse 1.0, 1.0, 1.0, 1.0
        texture_wrap true, true
        texture_transform {scale 5, 5, 5}
        texture "milkyway.jpg"
        rotatey 0.02 * page_time + 100
        scale 1, -1, 1
        sphere R
