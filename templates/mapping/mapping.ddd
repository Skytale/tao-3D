// ****************************************************************************
//  mapping.ddd                                                     Tao project
// ****************************************************************************
//
//   File Description:
//
//     Example of some texture mapping methods with Tao Presentations.
//
//
//
//
//
//
//
//
// ****************************************************************************
// This document is released under the GNU General Public License.
// See http://www.gnu.org/copyleft/gpl.html and Matthew 25:22 for details
//  (C) 1992-2010 Christophe de Dinechin <christophe@taodyne.com>
//  (C) 2010 Jérôme Forissier <jerome@taodyne.com>
//  (C) 2010 Catherine Burvelle <cathy@taodyne.com>
//  (C) 2010 Taodyne SAS
// ****************************************************************************


import Mapping


// ============================================================================
//
//                                   PAGES
//
// ============================================================================


page "Alpha Mapping",
// ----------------------------------------------------------------------------
//    Simulate transparent or translucent areas in an 3D object
// ----------------------------------------------------------------------------
    background 1.0
    title
    contents 100, 0,
        at_step 1
        +"A color map on texture unit 0"
    at_step 2
    map_highlight -150, 100, "metal.jpg"
    contents 100, -200,
        at_step 3
        +"An alpha map on texture unit 1"
    at_step 4
    map_highlight -150, -100, "alphaMap.jpg"
    at_step 5
    mapping
    at_step 6
    locally
        texture_unit 0
        texture "metal.jpg"
        texture_unit 1
        texture "alphaMap.jpg"
        alpha_mapping 0.15
        result_highlight 400, 0
        cube 0, 0, 0, 300, 300, 300

page "Normal Mapping",
// ----------------------------------------------------------------------------
//    Fake the lighting of bumps and dents on an object
// ----------------------------------------------------------------------------
    background 1.0
    title
    contents 100, 0,
        at_step 1
        +"A color map on texture unit 0"
    at_step 2
    map_highlight -150, 100, "face.jpg"
    contents 100, -200,
        at_step 3
        +"A normal map on texture unit 1"
    at_step 4
    map_highlight -150, -100, "normalMap.jpg"
    at_step 5
    mapping
    locally
        white_light 0, 0, 50
        material_ambient 0.3, 0.3, 0.3, 1
        material_diffuse 1.0, 1.0, 1.0, 1.0
        material_specular 0.3, 0.3, 0.3, 1.0
        material_shininess 70
        at_step 6
        color "white"
        texture_unit 0
        texture "face.jpg"
        texture_unit 1
        texture "normalMap.jpg"
        result_highlight 400, 0
        normal_mapping
        rectangle 0, 0, 300, 300

page "Displacement Mapping",
// ----------------------------------------------------------------------------
//    Modify geometry of an object in order to add details
// ----------------------------------------------------------------------------
    background 1.0
    title
    contents 100, 0,
        at_step 1
        +"A color map on texture unit 0"
    at_step 2
    map_highlight -120, 100, "colorMap.png"
    contents 100, -200,
        at_step 3
        +"A displacement map on texture unit 1"
    at_step 4
    map_highlight -120, -100, "displacementMap.png"
    at_step 5
    mapping
    locally
        at_step 6
        color "white"
        texture_unit 0
        texture "colorMap.png"
        texture_unit 1
        texture "displacementMap.png"
        result_highlight 400, 0
        displacement_mapping 100 * exp (-5 * (highest_step - smooth_step) ^ 2)
        plane 0, 0, 300, 300, 150, 150

page "Noise Mapping",
// ----------------------------------------------------------------------------
//    Add some noise on an objet
// ----------------------------------------------------------------------------
    background 1.0
    title
    contents 100, 0,
        at_step 1
        +"A color map on texture unit 0"
    at_step 2
    map_highlight -150, 100, "freeSky.jpg"
    contents 100, -200,
        at_step 3
        +"A noise map on texture unit 1"
    at_step 4
    map_highlight -150, -100, "noiseMap.jpg"
    at_step 5
    mapping
    at_step 6
    locally
        texture_unit 0
        texture "freeSky.jpg"
        texture_unit 1
        texture "noiseMap.jpg"
        result_highlight 400, 0
        noise_mapping 3 * sin time
        rectangle 0, 0, 300, 300

page "Sphere Mapping",
// ----------------------------------------------------------------------------
//   Simulate environment reflection with a sphere map
// ----------------------------------------------------------------------------
    background 1.0
    title
    contents 100, 0,
        at_step 1
        +"A color map on texture unit 0"
    at_step 2
    map_highlight -150, 100, "tiles.jpg"
    contents 100, -200,
        at_step 3
        +"A sphere map on texture unit 1"
    at_step 4
    map_highlight -150, -100, "sphereMap.jpg"
    at_step 5
    mapping
    at_step 6
    locally
        texture_unit 0
        texture "tiles.jpg"
        texture_unit 1
        texture "sphereMap.jpg"
        result_highlight 400, 0
        sphere_mapping 0.2
        sphere 0, 0, 0, 300


page "Cube Mapping",
// ----------------------------------------------------------------------------
//    Simulate environment reflection with a six-sided cube as the map shape﻿
// ----------------------------------------------------------------------------
    background 1.0
    title
    contents 100, 0,
        at_step 1
        +"A color map on texture unit 0"
    at_step 2
    map_highlight -150, 100, "tiles.jpg"
    contents 100, -200,
        at_step 3
        +"A cube map on texture unit 1"
    at_step 4
    locally
        // Transformation must be done before to apply the effect
        highlight -150, -100, factor (-150, -100), true
        color "white"
        cube_map 512,
            cube_map_flip false, false
            cube_map_face 0, "right.bmp"
            cube_map_face 1, "left.bmp"
            cube_map_face 2, "top.bmp"
            cube_map_face 3, "bottom.bmp"
            cube_map_face 4, "front.bmp"
            cube_map_face 5, "back.bmp"
        texture_transform
            scale 1, -1, 1
        cube 0, 0, 0, 100, 100, 100
    at_step 5
    mapping
    at_step 6
    locally
        texture_unit 0
        texture "tiles.jpg"
        texture_unit 1
        // Transformation must be done before to apply the effect
        result_highlight 400, 0
        cube_map 512,
            cube_map_flip false, false
            cube_map_face 0, "right.bmp"
            cube_map_face 1, "left.bmp"
            cube_map_face 2, "top.bmp"
            cube_map_face 3, "bottom.bmp"
            cube_map_face 4, "front.bmp"
            cube_map_face 5, "back.bmp"
        cube_mapping 0.3
        torus 0, 0, 0, 300, 300, 300, 40, 40, 0.5
