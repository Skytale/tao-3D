// Simple game to guess the random number

// Ask to refresh the screen after the click
refresh_on 2

// Internationalization
title_en -> "Select a flag to start"
title_fr -> "Sélectionnez un drapeau pour démarrer le jeu"
invit_en -> "Click on a circle"
invit_fr -> "Cliquez sur un cercle"
scramb_en -> "Scramble"
scramb_fr -> "Mélanger"
win_en    -> "You win !"
win_fr -> "Vous avez gagné !"
loose_en -> "You loose"
loose_fr -> "Vous avez perdu"

// Other variable definition
// String variables
font_color -> "font"
display -> ""
invit -> ""
scramble -> ""
win -> ""
loose -> ""

// Integer variables
result -> 0
clicked -> 0

// Real variables
x_flag -> 0.0
y_flag -> 0.0
w_flag -> 0.0
h_flag -> 0.0

// Definition to draw a clickable flag that set the international strings
flag Inv, Scramb, Win, Loose, Img ->
    active_widget
        color "white"
        line_width 1
        line_color "gray"
        image_px x_flag , y_flag, w_flag, h_flag, Img
        x_flag := x_flag + 1.5 * w_flag
        on_click
            invit := Inv
            scramble := Scramb
            win := Win
            loose := Loose
            reset
            goto_page "game"


// function to reset the game
reset ->
    font_color := "font"
    clicked := 0
    result := 0
    display := invit


// First page to choose the language
page "flags",
    w_flag := window_width / 4.0
    h_flag := window_width / 6.0
    x_flag := w_flag/4 - window_width / 4.0
    y_flag := 0.0
    flag invit_fr, scramb_fr, win_fr, loose_fr, "France.svg"
    flag invit_en, scramb_en, win_en, loose_en, "UnitedKingdom.svg"


// Second page where the game stand
page "game",
    x_flag := 100.0 - window_width / 2.0
    y_flag :=  window_height / 2.0 - 100.0
    w_flag := 100.0
    h_flag := 66.0
    flag invit_fr, scramb_fr, win_fr, loose_fr, "France.svg"
    flag invit_en, scramb_en, win_en, loose_en, "UnitedKingdom.svg"

    // This draw a clickable round rectangle, i.e. a button
    active_widget
        color "white"
        line_color "gray"
        line_width 1
        rounded_rectangle 0, 200, 200, 30, 15
        color "font"
        line_color "font"
        text_box 0, 200, 200, 30,
            align_center
            text scramble
        on_click
            reset
            result := random(1, 4)

    // Draw 3 green clickable circles
    color "lightgreen"
    active_widget
        circle -300, 0, 100
        on_click
            clicked := 1

    active_widget
        circle 0, 0, 100
        on_click
            clicked := 2

    active_widget
        circle 300, 0, 100
        on_click
            clicked := 3

    // Conditional part of the game :
    // Check if the clicked circle number is the same as the random number
    if clicked <> 0 then
        if result = clicked then
            display := win
            font_color := "green"
        else
            display := loose
            font_color := "red"

    // The text box that shows the result
    text_box 0, -200, 300, 20,
        align_center
        color font_color
        text display
