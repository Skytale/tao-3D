// ****************************************************************************
//  guess_game.ddd                                                  Tao project
// ****************************************************************************
//
//   File Description:
//
//     Example of guess game with Tao Presentations.
//
//
//
//
//
//
//
//
// ****************************************************************************
// Copyright Taodyne SAS, 2010
// Contributors:
//   Catherine Burvelle <cathy@taodyne.com>
//   Christophe de Dinechin <christophe@taodyne.com>
//   Jerome Forissier <jerome@taodyne.com>
//   Baptiste Soulisse <baptiste.soulisse@taodyne.com>
//
// <contact@taodyne.com>
//
// This software is a computer program whose purpose is to serve as a
// document template for the Tao Presentations application.
//
// This software is governed by the CeCILL license under French law and
// abiding by the rules of distribution of free software.  You can  use,
// modify and/ or redistribute the software under the terms of the CeCILL
// license as circulated by CEA, CNRS and INRIA at the following URL
// "http://www.cecill.info".
//
// As a counterpart to the access to the source code and  rights to copy,
// modify and redistribute granted by the license, users are provided only
// with a limited warranty  and the software's author,  the holder of the
// economic rights,  and the successive licensors  have only  limited
// liability.
//
// In this respect, the user's attention is drawn to the risks associated
// with loading,  using,  modifying and/or developing or reproducing the
// software by the user in light of its specific status of free software,
// that may mean  that it is complicated to manipulate,  and  that  also
// therefore means  that it is reserved for developers  and  experienced
// professionals having in-depth computer knowledge. Users are therefore
// encouraged to load and test the software's suitability as regards their
// requirements in conditions enabling the security of their systems and/or
// data to be ensured and,  more generally, to use and operate it in the
// same conditions as regards security.
//
// The fact that you are presently reading this means that you have had
// knowledge of the CeCILL license and that you accept its terms.
// ****************************************************************************


// ============================================================================
//
//                             GLOBAL SETTINGS
//
// ============================================================================


// Ask to refresh the screen after the click
refresh_on 2

// Disable selection rectangle
enable_selection_rectangle false

// Internationalization
title_en -> "Select a flag to start"
title_fr -> "Sélectionnez un drapeau pour démarrer le jeu"
invit_en -> "Click on a circle"
invit_fr -> "Cliquez sur un cercle"
scramb_en -> "Scramble"
scramb_fr -> "Mélanger"
win_en    -> "You win !"
win_fr -> "Vous avez gagné !"
loose_en -> "You loose"
loose_fr -> "Vous avez perdu"

// Other variable definition
// String variables
font_color -> "font"
display -> ""
invit -> ""
scramble -> ""
win -> ""
loose -> ""

// Integer variables
result -> 0
clicked -> 0

// Real variables
x_flag -> 0.0
y_flag -> 0.0
w_flag -> 0.0
h_flag -> 0.0


// ============================================================================
//
//                                   PAGES
//
// ============================================================================


page "Flags",
// -------------------------------------------------
//    Choose the language
// -------------------------------------------------
    w_flag := window_width / 4.0
    h_flag := window_width / 6.0
    x_flag := w_flag/4 - window_width / 4.0
    y_flag := 0.0
    flag invit_fr, scramb_fr, win_fr, loose_fr, "France.svg"
    flag invit_en, scramb_en, win_en, loose_en, "UnitedKingdom.svg"

page "Game",
// -------------------------------------------------
//    Play to the game
// -------------------------------------------------
    x_flag := 100.0 - window_width / 2.0
    y_flag :=  window_height / 2.0 - 100.0
    w_flag := 100.0
    h_flag := 66.0
    flag invit_fr, scramb_fr, win_fr, loose_fr, "France.svg"
    flag invit_en, scramb_en, win_en, loose_en, "UnitedKingdom.svg"

    // This draw a clickable round rectangle, i.e. a button
    active_widget
        color "white"
        line_color "gray"
        line_width 1
        rounded_rectangle 0, 200, 200, 30, 15
        color "font"
        line_color "font"
        text_box 0, 200, 200, 30,
            align_center
            text scramble
        on_click
            reset
            result := random(1, 4)

    // Draw 3 green clickable circles
    color "lightgreen"
    active_widget
        circle -300, 0, 100
        on_click
            clicked := 1

    active_widget
        circle 0, 0, 100
        on_click
            clicked := 2

    active_widget
        circle 300, 0, 100
        on_click
            clicked := 3

    // Conditional part of the game :
    // Check if the clicked circle number is the same as the random number
    if clicked <> 0 then
        if result = clicked then
            display := win
            font_color := "green"
        else
            display := loose
            font_color := "red"

    // The text box that shows the result
    text_box 0, -200, 300, 50,
        align_center
        color font_color
        text display


// ============================================================================
//
//                                 FUNCTIONS
//
// ============================================================================


flag Inv, Scramb, Win, Loose, Img ->
// -------------------------------------------------
//    Draw a clickable flag that set
//    the international strings
// -------------------------------------------------
    active_widget
        color "white"
        line_width 1
        line_color "gray"
        image_px x_flag , y_flag, w_flag, h_flag, Img
        x_flag := x_flag + 1.5 * w_flag
        on_click
            invit := Inv
            scramble := Scramb
            win := Win
            loose := Loose
            reset
            goto_page "Game"

reset ->
// -------------------------------------------------
//    Function to reset the game
// -------------------------------------------------
    font_color := "font"
    clicked := 0
    result := 0
    display := invit
