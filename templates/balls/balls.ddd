// ****************************************************************************
//  balls.ddd                                                       Tao project
// ****************************************************************************
//
//   File Description:
//
//     Example of use case of some materials effect
//     with Tao Presentations.
//
//
//
//
//
//
//
//
// ****************************************************************************
// Copyright Taodyne SAS, 2011
// Contributors:
//   Catherine Burvelle <cathy@taodyne.com>
//   Christophe de Dinechin <christophe@taodyne.com>
//   Jerome Forissier <jerome@taodyne.com>
//
// <contact@taodyne.com>
//
// This software is a computer program whose purpose is to serve as a
// document template for the Tao Presentations application.
//
// This software is governed by the CeCILL-B license under French law and
// abiding by the rules of distribution of free software.  You can  use,
// modify and/ or redistribute the software under the terms of the CeCILL-B
// license as circulated by CEA, CNRS and INRIA at the following URL
// "http://www.cecill.info".
//
// As a counterpart to the access to the source code and  rights to copy,
// modify and redistribute granted by the license, users are provided only
// with a limited warranty  and the software's author,  the holder of the
// economic rights,  and the successive licensors  have only  limited
// liability.
//
// In this respect, the user's attention is drawn to the risks associated
// with loading,  using,  modifying and/or developing or reproducing the
// software by the user in light of its specific status of free software,
// that may mean  that it is complicated to manipulate,  and  that  also
// therefore means  that it is reserved for developers  and  experienced
// professionals having in-depth computer knowledge. Users are therefore
// encouraged to load and test the software's suitability as regards their
// requirements in conditions enabling the security of their systems and/or
// data to be ensured and,  more generally, to use and operate it in the
// same conditions as regards security.
//
// The fact that you are presently reading this means that you have had
// knowledge of the CeCILL-B license and that you accept its terms.
// ****************************************************************************


import Mapping
import Materials
import Animate


// ============================================================================
//
//                             GLOBAL SETTINGS
//
// ============================================================================


zoom 0.25

Speed -> 1
Style -> "Refraction"

D -> window_width * 2
P -> 1.5 * D
R -> window_width / 8

// Keyboard
key "F1" ->
    Style := "Refraction"
key "F2" ->
    Style := "Reflection"
key "Right" ->
    skip 1
key "Left" ->
    skip -1 


// ============================================================================
//
//                                   PAGES
//
// ============================================================================


page "Intro",
// -------------------------------------------------
//    Intro scene
// -------------------------------------------------
    color "white"
    environment_cube Style
    show fade_between (page_time, 1.0, 4.0)
    HUD
    if page_time > 5.0 then
        goto_page "Spheres"

page "Spheres",
// -------------------------------------------------
//    Main scene
// -------------------------------------------------
    refresh_on TimerEvent
    color "white"
    Camera
    environment_cube Style
    light 0
    spheres 20


// ============================================================================
//
//                                   HELP
//
// ============================================================================


description_text "fr" ->
// -------------------------------------------------
//    French help
// -------------------------------------------------
    text "Utilisez les touches suivantes pour changer le matériau :"
    paragraph_break
    text " * F1 : verre"
    paragraph_break
    text " * F2 : métal"

description_text T:text ->
// -------------------------------------------------
//    English help
// -------------------------------------------------
    text "Press key to change material:"
    paragraph_break
    text " * F1 for glass"
    paragraph_break
    text " * F2 for metal"

HUD ->
// -------------------------------------------------
//    Head Up Display
// -------------------------------------------------
    color "black"
    camera_position 0, 0, 3200
    translatez 2500
    no_refresh_on MouseMoveEvent
    z_near 1
    text_box 0, 0, window_width * 75%, window_height,
        font "Orbitron", 60 * (window_width / 1200)
        vertical_align_center
        align_center
        description_text tao_language


// ============================================================================
//
//                                 FUNCTIONS
//
// ============================================================================


environment_cube "Refraction" ->
// -------------------------------------------------
//    Refraction environment
// -------------------------------------------------
    locally
        cmap_refract
        cube 0, 0, 0, 50000, 50000, 50000

environment_cube "Reflection" ->
// -------------------------------------------------
//    Reflection environment
// -------------------------------------------------
    locally
        cmap_reflect
        cube 0, 0, 0, 50000, 50000, 50000

cmap_refract ->
// -------------------------------------------------
//    Cube map for refraction
//    Images by Jochum Skoglund
// -------------------------------------------------
    color "white"
    cube_map 1024,
        cube_map_flip true, true
        cube_map_face 0, "refract/nx.jpg" // right
        cube_map_face 1, "refract/px.jpg" // left
        cube_map_face 2, "refract/py.jpg" // top
        cube_map_face 3, "refract/ny.jpg" // bottom
        cube_map_face 4, "refract/pz.jpg" // front
        cube_map_face 5, "refract/nz.jpg" // back

cmap_reflect ->
// -------------------------------------------------
//    Cube map for reflection
//    Images by Paul Debevec
// -------------------------------------------------
    color "white"
    cube_map 1024,
        cube_map_flip true, true
        cube_map_face 0, "reflect/nx.png" // right
        cube_map_face 1, "reflect/px.png" // left
        cube_map_face 2, "reflect/py.png" // top
        cube_map_face 3, "reflect/ny.png" // bottom
        cube_map_face 4, "reflect/pz.png" // front
        cube_map_face 5, "reflect/nz.png" // back

Camera ->
// -------------------------------------------------
//    Set camera position
// -------------------------------------------------
    Cx -> -230.856
    Cy -> -341.539
    locally
        Sx -> screen_mouse_x - window_width / 2
        Sy -> screen_mouse_y - window_height / 2
        Cx := Cx + (Sx - Cx) * 0.05
        Cy := Cy + (Sy - Cy) * 0.05
        camera_position 10 * Cx, -10 * Cy, 3200
        refresh_on TimerEvent
        no_refresh_on MouseMoveEvent
        z_near 1

// "Spiral" spheres
tm_s -> page_time * Speed
spheres_spiral 0, Max -> nil
spheres_spiral N:integer, Max:integer ->
// -------------------------------------------------
//    Make distribution of the spheres
// -------------------------------------------------
    locally
        K -> 0.05
        K := (real N) / Max
        translate K * D * cos (tm_s + N * 0.8), K * D * sin (tm_s + N * 0.82), K * P * sin (tm_s + N * 0.8)
        if Style = "Reflection" then
            cmap_reflect
            fresnel_material 0.0, 1.12, 10.0
        else
            cmap_refract
            fresnel_material 0.0, 1.12, 0.01
        sphere 0, 0, 0, R * (1 + 0.3 * (N mod 3))
    spheres_spiral N - 1, Max

spheres N:integer ->
// -------------------------------------------------
//    Draw spheres
// -------------------------------------------------
    spheres_spiral N, N
