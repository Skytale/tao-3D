// ****************************************************************************
//  YouCube.ddd                                                     Tao project
// ****************************************************************************
//
//   File Description:
//
//        Example displaying six YouTube videos on a rotating cube.
//
//
//
//
//
// ****************************************************************************
// Copyright Taodyne SAS, 2011-2012
// Contributors:
//   Catherine Burvelle <cathy@taodyne.com>
//   Christophe de Dinechin <christophe@taodyne.com>
//   Jerome Forissier <jerome@taodyne.com>
//
// <contact@taodyne.com>
//
// This software is a computer program whose purpose is to serve as a
// document template for the Tao Presentations application.
//
// This software is governed by the CeCILL license under French law and
// abiding by the rules of distribution of free software.  You can  use,
// modify and/ or redistribute the software under the terms of the CeCILL
// license as circulated by CEA, CNRS and INRIA at the following URL
// "http://www.cecill.info".
//
// As a counterpart to the access to the source code and  rights to copy,
// modify and redistribute granted by the license, users are provided only
// with a limited warranty  and the software's author,  the holder of the
// economic rights,  and the successive licensors  have only  limited
// liability.
//
// In this respect, the user's attention is drawn to the risks associated
// with loading,  using,  modifying and/or developing or reproducing the
// software by the user in light of its specific status of free software,
// that may mean  that it is complicated to manipulate,  and  that  also
// therefore means  that it is reserved for developers  and  experienced
// professionals having in-depth computer knowledge. Users are therefore
// encouraged to load and test the software's suitability as regards their
// requirements in conditions enabling the security of their systems and/or
// data to be ensured and,  more generally, to use and operate it in the
// same conditions as regards security.
//
// The fact that you are presently reading this means that you have had
// knowledge of the CeCILL license and that you accept its terms.
// ****************************************************************************


import VLCAudioVideo
import Animate


// ============================================================================
//
//                             GLOBAL SETTINGS
//
// ============================================================================


AngleX -> 0.0
SpeedX -> 1.0
MouseX0 -> 0.0
AngleY -> 0.0
SpeedY -> 1.0
MouseY0 -> 0.0

key "Return" -> movie_only ""

// Video quality
FMT -> "&fmt=5"  // Low bitrate
//FMT -> ""      // Best quality

// Avoid creating too many threads (1 per video)
vlc_init "--ffmpeg-threads", "1"

// ============================================================================
//
//                                   PAGES
//
// ============================================================================

page "This page intentionally left blank",
    clear_color 0, 0, 0, 1
    text_box 0, 0, 800, 600,
        color "white"
        align 0.5
        vertical_align 0.5
        font "Arial", 40
        text page_label
        line_break
        text "Press the 'down' key to get started"
    movie_only ""

page "YouCube Scrat!",
    Scrat2 -> "http://www.youtube.com/watch?v=RbsGWNXZ51I" & FMT
    Scrat3 -> "http://www.youtube.com/watch?v=n0ykM1ALggE" & FMT
    IceAge2 -> "http://www.youtube.com/watch?v=s0ITs4W3yjA" & FMT
    IceAge3 -> "http://www.youtube.com/watch?v=dfjcECDxuy4" & FMT
    IceAge4_1 -> "http://www.youtube.com/watch?v=TzzGPfVx32M" & FMT
    IceAge4_2 -> "http://www.youtube.com/watch?v=s-lEMsml6IE" & FMT
    video_cube Scrat2, Scrat3, IceAge2, IceAge3, IceAge4_1, IceAge4_2


// ============================================================================
//
//                                 FUNCTIONS
//
// ============================================================================


play V:text, AX:real, AY:real, Pass:integer -> play V, AX, AY, Pass, 600, 310

play V:text, AX:real, AY:real, Pass:integer, Size:real, Depth:real ->
    dcos A:real -> cos (A * pi/180)
    volume A:real -> 0.5 + 0.5 * dcos A

    locally
        rotatey AY
        rotatex AX
        translatez Depth
        // We do this to draw the translucent rectangles after the pictures
        if Pass < 0 then
            locally
                color "white"
                if movie_time V > 0 and not movie_playing V then
                    movie_drop V
                movie_texture V 
                movie_set_volume V, volume (AngleX+AX) * volume(AngleY+AY)
                translatez 1  // Avoid z-fighting
                rectangle 0, 0, Size, Size * texture_height / (texture_width+1)
            text_box 0, 0, Size, Size,
                align_center
                vertical_align_bottom
                color "white"
                font "Ubuntu", 20
                text V 
                line_break
                text "Time=" & text movie_time V
                text (if movie_playing V then " playing" else " not playing")
                text (if movie_done V then " done" else " not done")
        else
            locally
                disable_depth_mask
                color "lightblue", 0.2
                line_color "darkgrey", 0.2
                line_width 3
                rectangle 0, 0, Size, Size
                enable_depth_mask

video_cube V1:text, V2:text, V3:text, V4:text, V5:text, V6:text -> 
    enable_selection_rectangle false
    clear_color 0, 0, 0.1, 1

    scale3 S:real -> scale S, S, S
    scale3 window_height / 1200

    if mouse_buttons <> 0 then
        SpeedY := (mouse_x - MouseX0) * 0.01
        SpeedX := (MouseY0 - mouse_y) * 0.01
    else
        MouseX0 := mouse_x
        MouseY0 := mouse_y

    if page_time < 0.2 then
        movie_only ""

    locally 
        translate 300 * sin (0.05 * time) , 200 * cos (0.03317 * time) , -8000 
        color "white", 0.3 
        image 0, 0, 8, 8, "Clouds.png"

    locally
        translatez 1000
        rotatex AngleX
        rotatey AngleY

        play V1, 0, 0, -1
        play V2, 0, 90, -1
        play V3, 0, 180, -1
        play V4, 0, -90, -1
        play V5, 90, 0, -1
        play V6, -90, 0, -1

        play V1, 0, 0, 1
        play V2, 0, 90, 1
        play V3, 0, 180, 1
        play V4, 0, -90, 1
        play V5, 90, 0, 1
        play V6, -90, 0, 1

    AngleX := AngleX + SpeedX
    AngleY := AngleY + SpeedY
    SpeedX := SpeedX * (sin AngleX * 0.5 + 0.5)
    SpeedY := SpeedY * 0.95
    time
