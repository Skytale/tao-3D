import Materials 1.0
import Mapping 1.0
import ObjectLoader 1.0
zoom 0.15
refresh_on TimerEvent

/*
* Define cube map textures.
* Images from http://www.codemonsters.de/home/content.php?show=cubemaps
*/
RIGHT  -> "brightday2_negative_x.png"
LEFT   -> "brightday2_positive_x.png"
TOP    -> "brightday2_positive_y.png"
BOTTOM -> "brightday2_negative_y.png"
FRONT  -> "brightday2_positive_z.png"
BACK   -> "brightday2_negative_z.png"

mouse_move ->
    /*
    *  Define movement of the mouse
    */
    Cx -> 0.0
    Cy -> 0.0
    Sx -> screen_mouse_x - window_width / 2
    Sy -> screen_mouse_y - window_height / 2
    Cx := Cx + (Sx - Cx) * 0.005

    if (Cy < 10000) and (Cy > -10000) then
        Cy := Cy - Sy * 0.1
    else
        Cy := Cy + Sy * 0.1

    camera_position 14000 * sin (0.005 * Cx), Cy, 14000 * cos (0.005 * Cx)
    camera_target 0, 500, 0
    no_refresh_on MouseMoveEvent

draw_environment ->
    /*
    *  Define cube map environment
    */
    z_far 100000
    color "white"
    cube_map 1024,
        cube_map_flip true, true
        cube_map_face 0, RIGHT
        cube_map_face 1, LEFT
        cube_map_face 2, TOP
        cube_map_face 3, BOTTOM
        cube_map_face 4, FRONT
        cube_map_face 5, BACK
    cube 0, 0, 0, 60000, 60000, 60000

draw_light ->
    /*
    * Define a far light
    */
    light 0
    light_position -15000, 15000, 0
    light_ambient 0.5, 0.5, 0.5, 1.0
    light_diffuse 1.0, 1.0, 1.0, 1.0
    light_specular 0.5, 0.5, 0.5, 1.0

draw_materials ->
    /*
    * Draw materials set
    */
    translatey 800

    // Middle circle
    locally
        jade
        sphere -3000, 0, 0, 500
        gold
        sphere -1500, 0, -2000, 500
        glass
        sphere -1500, 0, 2000, 500
        water
        sphere 1000, 0, 2000, 500
        emerald
        sphere 3000, 0, 0, 500
        ruby
        sphere 1000, 0, -2000, 500

    // Top circle
    locally
        wood 100
        sphere 0, 750, 1250, 500
        granite 100
        sphere -1250, 750, -750, 500
        marble 100
        sphere 1250, 750, -750, 500

    // Bottom circle
    locally
        bronze
        sphere 0, -750, -750, 500
        silver
        sphere -1250, -750, 1250, 500
        iron
        sphere 1250, -750, 1250, 500

    // Top final
    locally
        plastic 1.0, 0.5, 0.5
        sphere 0, 1500, 0, 500

    // Bottom final
    locally
        chrome
        sphere 0, -1500, 0, 500

draw_environment
draw_light
mouse_move
draw_materials
