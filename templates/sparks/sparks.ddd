/**
* This document presents an example of the sparks module adapted from
* a shader code written by AlteredQualia (http://alteredqualia.com/)
* for the WebGL demo Sparks.
**/
import Sparks
import Animate

key "Return" -> goto_page page_name 1

page "Intro",
    compute_smooth_step
    draw_background
    locally
        show fade_between (page_time, 1.0, 4.0)
        color 0.1, 0.1, 0.1, 0.8
        fullscreen_quad
        text_box 0, 0, window_width * 75%, window_height,
            color "white"
            font "Ubuntu", 72 * (window_width / 1200)
            vertical_align_center
            align_center
            description_text tao_language

description_text "fr" ->
    text "Effet obtenu grâce à un shader GLSL appelé "
    italic
    text "Sparks"
    plain
    text "écrit par AlteredQualia."

description_text T:text ->
    text "Background effect drawn by a GLSL shader called "
    italic
    text "Sparks"
    paragraph_break
    plain
    text "written by AlteredQualia."

fullscreen_quad -> rectangle 0, 0, window_width, window_height

draw_background ->
    locally
        color 1.0 * abs (cos (time * 0.1)), 1.0, 1.0 * abs (sin (time * 0.1)), 1.0
        sparks_color 0.1, 0.1, 0.1, 1.0
        sparks page_time * 0.1, 0.05, 0.05
        fullscreen_quad
