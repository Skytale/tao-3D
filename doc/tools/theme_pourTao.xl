// ******************************************************************************
//  theme.xl                                                         Tao project
// ******************************************************************************
// File Description:
// Theme for Tao documentation
// ******************************************************************************
// This document is released under the GNU General Public License.
// See http://www.gnu.org/copyleft/gpl.html and Matthew 25:22 for details
// (C) 2011 Catherine Burvelle <catherine@taodyne.com>
// (C) 2011 Taodyne SAS
// ******************************************************************************

font "Title" -> font "Helvetica Neue", 24, slant 0% , weight 80% , stretch 100% 
font "Footer" -> font "Andika Basic", 12 
font "Text" -> font "Helvetica Neue", 18, slant 0% , weight 50% , stretch 100% 
font "Item" -> font "Helvetica Neue", 20, slant 0% , weight 80% , stretch 100% 
font "Code" -> font "Courier", 18, slant 0% , weight 50% , stretch 100% 
font "Type" -> font "Helvetica Neue", 18, slant 50% , weight 40% , stretch 100% 
color "Accent", 5 -> color 20% , 60% , 100% , 100% 
color "font" -> color 0% , 0% , 0% , 100% 
line_color "font" -> line_color 0% , 0% , 0% , 0% 
color "Text" "Light" -> color 0% , 0% , 0% , 100% 
family     -> ""
familyList -> ""

groupname familyName:text -> 
    family := familyName
    page familyName, do 
        color 100% , 100% , 100% , 100% 
        shape 
            url -152, -4, 1113, 1022, "/Users/cathy/Work/tao.git/tao/doc/src/" & familyName & ".html" 
    import "all_documentation.xl" 

docname fname:text, families:text, t:tree -> 
    familyList := families
    isContained -> familyList contains family
    if isContained >= 0 then
        page fname, do 
            page_link "Backspace", "TOC" 
            text_box 0, -200, 300, 80, do 
                color "Accent", 5 
                font "Footer"
                text "Group :" & familyList
                text " - page " & text page_number & " on " & text page_count 
            //image -200, -300, ""
            color "font" 
            line_color "font" 
            font "Times New Roman", 18, slant 0% , weight 50% , stretch 100% 
            text_box 0, 300, 1000, 600, do 
                paragraph_space 0, 10 
                font "Title" 
                color "Accent", 5 
                text fname 
                paragraph_break 
                color "font" 
                line_color "font" 
                do t 


dsyntax text_syntax -> 
    font "Item" 
    paragraph_break 
    text "Syntax: " 
    font "Code" 
    text text_syntax 
    paragraph_break 

synopsis text_synopsis -> 
    font "Item" 
    line_break 
    text "Synopsis: " 
    font "Text" 
    text text_synopsis 
    paragraph_break 

description text_desc -> 
    font "Item" 
    line_break 
    text "Description: " 
    font "Text" 
    text text_desc 
    paragraph_break 

parameters -> 
    font "Item" 
    line_break 
    text "Parameters: " 

parameter ptype, pname, pdesc -> 
    line_break 
    factor -> 18.0 
    font "Text" 
    font_size factor 
    margins 40, 0 
    paragraph_space 0, 10 
    paragraph_break 
    anchor 
        color "Accent", 5 
        circle factor * -75% , factor * 30% , factor * 20% 

    color "Text" "Light" 
    font "Item" 
    text pname 
    font "Type" 
    text " [" & ptype & "] " 
    font "Text" 
    text pdesc 
    margins 0, 0 

return_value rtype, rdesc -> 
    font "Item" 
    paragraph_break 
    text "Returns: " 
    font "Type" 
    text "[" & rtype & "] " 
    font "Text" 
    text rdesc 
    paragraph_break 

see text_ref -> 
    font "Item" 
    paragraph_break 
    text "See: " 
    anchor 
        factor -> 18.0 
        on_click goto_page text_ref 
        color "Accent", 5 
        circle factor * -75% , factor * 30% , factor * 20% 
    font "Text" 
    text text_ref 
    paragraph_break 

err etype, evalue -> 
    font "Item" 
    paragraph_break 
    text "error: " 
    font "Type" 
    text "[" & etype & "] " 
    font "Text" 
    text evalue 
    paragraph_break 
