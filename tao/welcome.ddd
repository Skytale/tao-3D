beta N:integer ->
    select_font 0 -> font "Ubuntu"
    select_font 1 -> font "Bentham"
    select_font 2 -> font "Ubuntu"
    select_font 3 -> font "Tangerine"
    select_font 4 -> font "Lobster"
    select_font 5 -> font "Droid Sans"
    select_font 6 -> font "UnifrakturMaguntia/Book"
    Nfonts -> 7
    locally
        W -> window_width
        H -> window_height
        Z -> 0.0
        Z := -177.0 * N
        Zcam -> camera_position at 3
        S -> (Zcam - Z)/Zcam
        WW -> S*(W+400)
        default_refresh 5.0
        translate (50 * page_time + (N * 451.0)) mod WW - WW/2, (N * 2311.0) mod H - H/2, Z
        text_box 0, 0, 300, 300,
            align_center
            vertical_align_center
            select_font (N mod Nfonts)
            if (N mod 2) = 0 then
                italic
            font_size 25 + (N * 37.0) mod 200
            color 0.0, 0.2, 1.0, 0.1 + 0.8/(1.0 - Z/400)
            text "Beta9"

beta with 1..30

text_box 0, 0, window_width, window_height,
    font "Ubuntu", 80, bold
    line_width 2 
    line_color "black" 
    align_center 
    vertical_align_center 
    rainbow 0, "Tao Presentations" 
    line_break 

    font "Arial", 20, italic 
    color "darkgrey" 
    line_width 0 
    text "Version " & tao_version
    line_break 
    text "Â© Copyright 2010-2011 Taodyne SAS" 

text_box 0, 0, window_width, window_height,
    vertical_align_bottom
    font "Arial", 28 
    align_center 
    color "white"
    line_color "black"
    line_width 1
    help_message tao_language

help_message "fr" ->
    text "L'aide en ligne se trouve dans le menu ""Aide"""

help_message T:text ->
    text "Online help is available in the ""Help"" menu"

rainbow Offset:integer, Message:text ->
    color_hsv Offset * 11 + seconds * 17, 0.5, 1, 1
    text text_range(Message, Offset, 1)
    if Offset < text_length Message then
        rainbow Offset+1, Message
