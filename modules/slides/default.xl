// ****************************************************************************
//  default.xl                                                     Tao project
// ****************************************************************************
//
//   File Description:
//
//     Define a default theme for slides.
//
//
//
//
//
//
//
//
// ****************************************************************************
// Copyright Taodyne SAS, 2012
// Contributors:
//   Baptiste Soulisse <baptiste.soulisse@taodyne.com>
//
// <contact@taodyne.com>
//
// This software is a computer program whose purpose is to serve as a
// document template for the Tao Presentations application.
//
// This software is governed by the CeCILL license under French law and
// abiding by the rules of distribution of free software.  You can  use,
// modify and/ or redistribute the software under the terms of the CeCILL
// license as circulated by CEA, CNRS and INRIA at the following URL
// "http://www.cecill.info".
//
// As a counterpart to the access to the source code and  rights to copy,
// modify and redistribute granted by the license, users are provided only
// with a limited warranty  and the software's author,  the holder of the
// economic rights,  and the successive licensors  have only  limited
// liability.
//
// In this respect, the user's attention is drawn to the risks associated
// with loading,  using,  modifying and/or developing or reproducing the
// software by the user in light of its specific status of free software,
// that may mean  that it is complicated to manipulate,  and  that  also
// therefore means  that it is reserved for developers  and  experienced
// professionals having in-depth computer knowledge. Users are therefore
// encouraged to load and test the software's suitability as regards their
// requirements in conditions enabling the security of their systems and/or
// data to be ensured and,  more generally, to use and operate it in the
// same conditions as regards security.
//
// The fact that you are presently reading this means that you have had
// knowledge of the CeCILL license and that you accept its terms.
// ****************************************************************************

// This file is designed to be overriden by themes, etc.
override_priority -1

// ============================================================================
// 
//    Color palette
// 
// ============================================================================

// A few colors we selected for the theme (these are from the Taodyne palette)
theme_color Theme:text, "red"       -> "#AA0022"
theme_color Theme:text, "orange"    -> "#DD3311"
theme_color Theme:text, "yellow"    -> "#EEAA11"
theme_color Theme:text, "green"     -> "#88BB00"
theme_color Theme:text, "ocean"     -> "#229988"
theme_color Theme:text, "sky"       -> "#3399FF"
theme_color Theme:text, "navy"      -> "#003366"
theme_color Theme:text, "purple"    -> "#CC0099"
theme_color Theme:text, "grey"      -> "#555"
theme_color Theme:text, "black"     -> "black"
theme_color Theme:text, "lightblue" -> "#3399FF"
theme_color Theme:text, "darkblue"  -> "#003366"
theme_color Theme:text, "darkgreen" -> "#006633"
theme_color Theme:text, "brown"     -> "#7E3830"
theme_color Theme:text, Color:text  -> Color



// ============================================================================
// 
//    Styles for the various elements
// 
// ============================================================================

theme_style Theme:text, "title" ->
// ----------------------------------------------------------------------------
//    Style for title in normal slides
// ----------------------------------------------------------------------------
    font "TeX Gyre Adventor", "Arial", 80, bold
    color theme_color (Theme, "brown")
    align 0.5
    vertical_align 0.5


theme_style Theme:text, "subtitle" ->
// ----------------------------------------------------------------------------
//    Style for section subtitle
// ----------------------------------------------------------------------------
    font "TeX Gyre Adventor", "Arial", 60, bold
    color theme_color (Theme, "navy")
    align 0.5
    vertical_align 0.5


theme_style Theme:text, "story" ->
// ----------------------------------------------------------------------------
//    Style for story text
// ----------------------------------------------------------------------------
    font "TeX Gyre Adventor", "Arial", 50
    color theme_color (Theme, "black")
    paragraph_space 30, 30
    align 0
    vertical_align 0.3


theme_style Theme:text, "main:title" ->
// ----------------------------------------------------------------------------
//   Style for main title
// ----------------------------------------------------------------------------
    color theme_color (Theme, "red")
    font "TeX Gyre Adventor", "Arial", 150, bold
    align 0.5
    vertical_align 0.5


theme_style Theme:text, "main:subtitle" ->
// ----------------------------------------------------------------------------
//   Style for main subtitle
// ----------------------------------------------------------------------------
    color theme_color (Theme, "brown")
    font "TeX Gyre Adventor", "Arial", 90, bold
    align 0.5
    vertical_align 0.5


theme_style Theme:text, "section:title" ->
// ----------------------------------------------------------------------------
//    Style for section title
// ----------------------------------------------------------------------------
    font "TeX Gyre Adventor", "Arial", 120, bold
    color theme_color (Theme, "red")
    align 0.5
    vertical_align 0.5


theme_style Theme:text, "section:subtitle" ->
// ----------------------------------------------------------------------------
//    Style for section subtitle
// ----------------------------------------------------------------------------
    font "TeX Gyre Adventor", "Arial", 85, bold
    color theme_color (Theme, "brown")
    align 0.5
    vertical_align 0.5


theme_style Theme:text, "picture" ->
// ----------------------------------------------------------------------------
//   Style for pictures
// ----------------------------------------------------------------------------
    color "white"


theme_style Theme:text, "border" ->
// ----------------------------------------------------------------------------
//   Border color for title pages
// ----------------------------------------------------------------------------
    color theme_color (Theme, "brown"), 0.3


theme_style Theme:text, "bullet:1" ->
// ----------------------------------------------------------------------------
//   Bullet color
// ----------------------------------------------------------------------------
    color theme_color (Theme, "red")


theme_style Theme:text, "bullet:2" ->
// ----------------------------------------------------------------------------
//   Bullet color
// ----------------------------------------------------------------------------
    color theme_color (Theme, "navy")


theme_style Theme:text, "bullet:3" ->
// ----------------------------------------------------------------------------
//   Bullet color
// ----------------------------------------------------------------------------
    color theme_color (Theme, "brown")


theme_style Theme:text, "bullet" ->
// ----------------------------------------------------------------------------
//   Bullet color
// ----------------------------------------------------------------------------
    color theme_color (Theme, "orange")


theme_style Theme:text, "box" ->
// ----------------------------------------------------------------------------
//   Style for text boxes
// ----------------------------------------------------------------------------
    color "transparent"
    line_color theme_color (Theme, "navy"), 0.6
    line_width 2



// ============================================================================
// 
//    Background pictures
// 
// ============================================================================

theme_background Theme:text, "main" ->
// ----------------------------------------------------------------------------
//    Default title background has rectangles at top and bottom
// ----------------------------------------------------------------------------
    clear_color 1, 1, 1, 1
    background -300,
        theme_style Theme, "border"
        rectangle 0, 0, slide_width * 1.1, slide_height * 0.9
        rectangle 0, slide_height * 0.5, slide_width * 1.1, slide_height * 0.2
        rectangle 0, -slide_height * 0.5, slide_width * 1.1, slide_height * 0.2


theme_background Theme:text, "section" ->
// ----------------------------------------------------------------------------
//    Default section background is the same
// ----------------------------------------------------------------------------
    clear_color 1, 1, 1, 1
    background -300,
        theme_style Theme, "border"
        rectangle 0, slide_height * 0.5, slide_width * 1.1, slide_height * 0.2
        rectangle 0, -slide_height * 0.5, slide_width * 1.1, slide_height * 0.2


theme_background Theme:text, Master:text ->
// ----------------------------------------------------------------------------
//    Default background is solid white with a vertical bar on the left
// ----------------------------------------------------------------------------
    clear_color 1, 1, 1, 1
    background -300,
        theme_style Theme, "border"
        rectangle -slide_width * 0.5, 0, slide_width * 0.1, slide_height * 1.1



// ============================================================================
// 
//    Layout for the various kind of pages
// 
// ============================================================================

theme_slide_width  Theme:text -> 1920
theme_slide_height Theme:text -> 1080

theme_layout Theme:text, "main" ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow 0, 0, 1800, 300, "title"
    flow 0, -300, 1800, 200, "subtitle"


theme_layout Theme:text, "section" ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow 0, 0, 1800, 300, "title"
    flow 0, -300, 1800, 200, "subtitle"


theme_layout Theme:text, Master:text ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow 0, 450, 1800, 140, "title"

    flow 0, -90, 1800, 850, "story"
    flow 0, -90, 1800, 850, "picture"
    flow -475, -90, 850, 850, "left"
    flow  475, -90, 850, 850, "right"



// ============================================================================
// 
//   Titles and subtitles
// 
// ============================================================================

theme_title Theme:text, "main", Body ->
// ----------------------------------------------------------------------------
//    Default main title (centered on the slide)
// ----------------------------------------------------------------------------
    text_flow "title",
        theme_style Theme, "main:title"
        Body


theme_subtitle Theme:text, "main", Body ->
// ----------------------------------------------------------------------------
//    Default main title (centered on the slide)
// ----------------------------------------------------------------------------
    text_flow "subtitle",
        theme_style Theme, "main:subtitle"
        Body


theme_title Theme:text, "section", Body ->
// ----------------------------------------------------------------------------
//    Default section title (centered on the slide)
// ----------------------------------------------------------------------------
    text_flow "title",
        theme_style Theme, "section:title"
        Body


theme_subtitle Theme:text, "section", Body ->
// ----------------------------------------------------------------------------
//    Default section title (centered on the slide)
// ----------------------------------------------------------------------------
    text_flow "subtitle",
        theme_style theme, "section:subtitle"
        Body


theme_title Theme:text, Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default title is at the top
// ----------------------------------------------------------------------------
    text_flow "title",
        theme_style theme, "title"
        Body


theme_subtitle Theme:text, Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default subtitle is below main title
// ----------------------------------------------------------------------------
    text_flow "title",
        theme_style Theme, "subtitle"
        Body



// ============================================================================
// 
//   Contents
// 
// ============================================================================

theme_story Theme:text, Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default story box
// ----------------------------------------------------------------------------
    theme_style Theme, "story"
    text_flow "story", Body


theme_left_column Theme:text, Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default left column
// ----------------------------------------------------------------------------
    theme_style Theme, "story"
    text_flow "left", Body


theme_right_column Theme:text, Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default right column
// ----------------------------------------------------------------------------
    theme_style Theme, "story"
    text_flow "right", Body


theme_picture Theme:text, Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default picture box
// ----------------------------------------------------------------------------
    contents 0,
        translatey -90
        theme_style Theme, "picture"
        Body


theme_left_column_picture Theme:text, Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default left column picture
// ----------------------------------------------------------------------------
    contents 0,
        translate -475, -90, 0
        theme_style Theme, "picture"
        Body


theme_right_column_picture Theme:text, Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default right column picture
// ----------------------------------------------------------------------------
    contents 0,
        translate 475, -90, 0
        theme_style Theme, "picture"
        Body



// ============================================================================
// 
//   Text boxes
// 
// ============================================================================

theme_bbox Theme:text, Mast:text, X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//   Draw the background box for text and flows
// ----------------------------------------------------------------------------
    contents -10,
        theme_style Theme, "box"
        rectangle X, Y, W + 10, H + 10


theme_box Theme:text, Master:text, X:real, Y:real, W:real, H:real, Body ->
// ----------------------------------------------------------------------------
//   Draw a default text box
// ----------------------------------------------------------------------------
    theme_bbox Theme, Master, X, Y, W, H
    contents 0,
        text_box X, Y, W, H, Body


theme_flow Theme:text, Master:text, X:real, Y:real, W:real, H:real, Flow:text ->
// ----------------------------------------------------------------------------
//   Draw a default text box containing a flow
// ----------------------------------------------------------------------------
    theme_bbox Theme, Master, X, Y, W, H
    contents 0,
        text_box Flow, X, Y, W, H



// ============================================================================
// 
//    Bullet formatting
// 
// ============================================================================

theme_bullet Theme:text, Master:text, Level:integer, Text:text ->
// ----------------------------------------------------------------------------
//   Default formatting for a bullet
// ----------------------------------------------------------------------------
    theme_bullet_style Theme, Master, Level
    paragraph_break
    anchor
        theme_bullet_color Theme, Master, Level
        theme_bullet_picture Theme, Master, Level
    text Text


theme_item Theme:text, Master:text, Level:integer, Text:text ->
// ----------------------------------------------------------------------------
//   Default formatting for a text item (without bullet)
// ----------------------------------------------------------------------------
    theme_bullet_style Theme, Master, Level
    paragraph_break
    text Text


theme_numbered_item Theme:text, Master:text, Level:integer, Text:text ->
// ----------------------------------------------------------------------------
//   Default formatting for a numbered text item
// ----------------------------------------------------------------------------
    theme_bullet_style Theme, Master, Level
    paragraph_break
    default_item_level := Level
    text text default_item_number[default_item_level] & ". "
    text Text
    default_item_number[default_item_level] := 1 + default_item_number[default_item_level]
    default_item_number[default_item_level+1] := 1


theme_reset Theme:text ->
// ----------------------------------------------------------------------------
//    Reset theme numbers
// ----------------------------------------------------------------------------
    default_item_number[1] := 1
    default_item_number[2] := 1
    default_item_number[3] := 1
    default_item_number[4] := 1
    default_item_number[5] := 1

default_item_level -> 1
data default_item_number


theme_bullet_size Theme:text, Master:text, 1 -> 62
theme_bullet_size Theme:text, Master:text, 2 -> 52
theme_bullet_size Theme:text, Master:text, Level:integer -> 42
// ----------------------------------------------------------------------------
//    Default size of a bullet
// ----------------------------------------------------------------------------


theme_bullet_color Theme:text, Master:text, 1 -> theme_style Theme, "bullet:1"
theme_bullet_color Theme:text, Master:text, 2 -> theme_style Theme, "bullet:2"
theme_bullet_color Theme:text, Master:text, 3 -> theme_style Theme, "bullet:3"
theme_bullet_color Theme:text, Master:text, N -> theme_style Theme, "bullet"
// ----------------------------------------------------------------------------
//    Default bullet fill colors
// ----------------------------------------------------------------------------


theme_bullet_style Theme:text, Master:text, Level:integer ->
// ----------------------------------------------------------------------------
//   Default style of a bullet point
// ----------------------------------------------------------------------------
    plain
    theme_style Theme, "story"
    margins Level * theme_bullet_size(Theme, Master, Level), 0
    font_size theme_bullet_size (Theme, Master, Level)


theme_bullet_picture Theme:text, Master:text, Level:integer ->
// ----------------------------------------------------------------------------
//    Default style of a bullet
// ----------------------------------------------------------------------------
    theme_bullet_circle S:real -> circle -50% * S, 30% * S, 12% * S
    theme_bullet_circle theme_bullet_size (Theme, Master, Level)
