// ****************************************************************************
//  default.xl                                                     Tao project
// ****************************************************************************
//
//   File Description:
//
//     Define a default theme for slides.
//
//
//
//
//
//
//
//
// ****************************************************************************
// Copyright Taodyne SAS, 2012
// Contributors:
//   Baptiste Soulisse <baptiste.soulisse@taodyne.com>
//
// <contact@taodyne.com>
//
// This software is a computer program whose purpose is to serve as a
// document template for the Tao Presentations application.
//
// This software is governed by the CeCILL license under French law and
// abiding by the rules of distribution of free software.  You can  use,
// modify and/ or redistribute the software under the terms of the CeCILL
// license as circulated by CEA, CNRS and INRIA at the following URL
// "http://www.cecill.info".
//
// As a counterpart to the access to the source code and  rights to copy,
// modify and redistribute granted by the license, users are provided only
// with a limited warranty  and the software's author,  the holder of the
// economic rights,  and the successive licensors  have only  limited
// liability.
//
// In this respect, the user's attention is drawn to the risks associated
// with loading,  using,  modifying and/or developing or reproducing the
// software by the user in light of its specific status of free software,
// that may mean  that it is complicated to manipulate,  and  that  also
// therefore means  that it is reserved for developers  and  experienced
// professionals having in-depth computer knowledge. Users are therefore
// encouraged to load and test the software's suitability as regards their
// requirements in conditions enabling the security of their systems and/or
// data to be ensured and,  more generally, to use and operate it in the
// same conditions as regards security.
//
// The fact that you are presently reading this means that you have had
// knowledge of the CeCILL license and that you accept its terms.
// ****************************************************************************


// A few colors we selected for the theme (these are from the Taodyne palette)
theme_color "default:red"       -> "#AA0022"
theme_color "default:orange"    -> "#DD3311"
theme_color "default:yellow"    -> "#EEAA11"
theme_color "default:green"     -> "#88BB00"
theme_color "default:ocean"     -> "#229988"
theme_color "default:sky"       -> "#3399FF"
theme_color "default:navy"      -> "#003366"
theme_color "default:purple"    -> "#CC0099"
theme_color "default:grey"      -> "#555"
theme_color "default:lightblue" -> "#3399FF"
theme_color "default:darkblue"  -> "#003366"
theme_color "default:darkgreen" -> "#006633"
theme_color "default:blue1"     -> "#D0EAF0"
theme_color "default:blue2"     -> "#8FB4CB"
theme_color "default:blue3"     -> "#7AA9C1"
theme_color "default:blue4"     -> "#1D456B"
theme_color "default:brown"     -> "#7E3830"


// Colors that we will use as primary, secondary, text and borders
theme_item_color "default:text"      -> color "default:black"
theme_item_color "default:main"      -> color theme_color "default:red"
theme_item_color "default:primary"   -> color theme_color "default:brown"
theme_item_color "default:secondary" -> color theme_color "default:red"
theme_item_color "default:highlight" -> color theme_color "default:navy"
theme_item_color "default:border"    -> color theme_color "default:brown", 0.3
theme_item_color "default:box"       -> color "transparent"; line_color theme_color "default:navy", 0.6; line_width 2


// ============================================================================
// 
//    Background pictures
// 
// ============================================================================

theme_background "default", "main" ->
// ----------------------------------------------------------------------------
//    Default title background has rectangles at top and bottom
// ----------------------------------------------------------------------------
    clear_color 1, 1, 1, 1
    background -300,
        theme_item_color "default:border"
        rectangle 0, 0, slide_width * 1.1, slide_height * 0.9
        rectangle 0, slide_height * 0.5, slide_width * 1.1, slide_height * 0.2
        rectangle 0, -slide_height * 0.5, slide_width * 1.1, slide_height * 0.2


theme_background "default", "section" ->
// ----------------------------------------------------------------------------
//    Default section background is the same
// ----------------------------------------------------------------------------
    clear_color 1, 1, 1, 1
    background -300,
        theme_item_color "default:border"
        rectangle 0, slide_height * 0.5, slide_width * 1.1, slide_height * 0.2
        rectangle 0, -slide_height * 0.5, slide_width * 1.1, slide_height * 0.2


theme_background "default", Master:text ->
// ----------------------------------------------------------------------------
//    Default background is solid white with a vertical bar on the left
// ----------------------------------------------------------------------------
    clear_color 1, 1, 1, 1
    background -300,
        theme_item_color "default:border"
        rectangle -slide_width * 0.5, 0, slide_width * 0.1, slide_height * 1.1



// ============================================================================
// 
//    Layout for the various kind of pages
// 
// ============================================================================

theme_layout "default", "main" ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow 0, 0, 1800, 400, "title"
    flow 0, -400, 1800, 300, "subtitle"


theme_layout "default", "section" ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow 0, 0, 1800, 400, "title"
    flow 0, -400, 1800, 300, "subtitle"


theme_layout "default", Master:text ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow 0, 470, 1800, 250, "title"

    flow 0, -150, 1800, 900, "story"
    flow 0, -150, 1800, 900, "picture"
    flow -475, -150, 850, 900, "left"
    flow  475, -150, 850, 900, "right"
    flow -475, -150, 850, 900, "leftpic"
    flow  475, -150, 850, 900, "rightpic"



// ============================================================================
// 
//    Style for the various components
// 
// ============================================================================

theme_style "default", Master:text ->
// ----------------------------------------------------------------------------
//   Select the style for a given box
// ----------------------------------------------------------------------------
    font "TeX Gyre Adventor", "Arial", 80, bold
    align 0.0
    vertical_align 0.3




// ============================================================================
// 
//   Titles and subtitles
// 
// ============================================================================

theme_title "default", "main", Body ->
// ----------------------------------------------------------------------------
//    Default main title (centered on the slide)
// ----------------------------------------------------------------------------
    text_flow "title",
        font "TeX Gyre Adventor", "Arial", 150, bold
        theme_item_color "default:main"
        align 0.5
        vertical_align 0.5
        Body


theme_subtitle "default", "main", Body ->
// ----------------------------------------------------------------------------
//    Default main title (centered on the slide)
// ----------------------------------------------------------------------------
    text_flow "subtitle",
        font "TeX Gyre Adventor", "Arial", 90, bold
        theme_item_color "default:main"
        align 0.5
        paragraph_break
        Body


theme_title "default", "section", Body ->
// ----------------------------------------------------------------------------
//    Default section title (centered on the slide)
// ----------------------------------------------------------------------------
    text_flow "title",
        font "TeX Gyre Adventor", "Arial", 120, bold
        theme_item_color "default:primary"
        align 0.5
        vertical_align 0.5
        Body


theme_subtitle "default", "section", Body ->
// ----------------------------------------------------------------------------
//    Default section title (centered on the slide)
// ----------------------------------------------------------------------------
    text_flow "subtitle",
        font "TeX Gyre Adventor", "Arial", 85, bold
        theme_item_color "default:secondary"
        align 0.5
        paragraph_break
        Body


theme_title "default", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default title is at the top
// ----------------------------------------------------------------------------
    text_flow "title",
        font "TeX Gyre Adventor", "Arial", 80, bold
        theme_item_color "default:primary"
        align 0.5
        vertical_align 0.5
        Body


theme_subtitle "default", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default subtitle is below main title
// ----------------------------------------------------------------------------
    text_flow "title",
        font "TeX Gyre Adventor", "Arial", 60, bold
        theme_item_color "default:highlight"
        align 0.5
        paragraph_break
        Body



// ============================================================================
// 
//   Contents
// 
// ============================================================================

theme_picture "default", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default picture box
// ----------------------------------------------------------------------------
    contents 0,
        translatey -150
        color "default:white"
        Body


theme_story "default", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default story box
// ----------------------------------------------------------------------------
    theme_style "default", Master
    text_flow "story", Body


theme_left_column "default", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default left column
// ----------------------------------------------------------------------------
    theme_style "default", Master
    text_flow "left", Body


theme_right_column "default", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default right column
// ----------------------------------------------------------------------------
    theme_style "default", Master
    text_flow "right", Body


theme_left_column_picture "default", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default left column picture
// ----------------------------------------------------------------------------
    contents 0,
        translate -475, -150, 0
        Body


theme_right_column_picture "default", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Default right column picture
// ----------------------------------------------------------------------------
    contents 0,
        translate 475, -150, 0
        Body



// ============================================================================
// 
//   Text boxes
// 
// ============================================================================

theme_box "default", Master:text, X:real, Y:real, W:real, H:real, Body ->
// ----------------------------------------------------------------------------
//   Draw a default text box
// ----------------------------------------------------------------------------
    contents -10,
        theme_item_color "default:box"
        rectangle X, Y, W + 10, H + 10
    contents 0,
        text_box X, Y, W, H, Body


theme_flow "default", Master:text, X:real, Y:real, W:real, H:real, Flow ->
// ----------------------------------------------------------------------------
//   Draw a default text box containing a flow
// ----------------------------------------------------------------------------
    contents -10,
        theme_item_color "default:box"
        rectangle X, Y, W + 10, H + 10
    contents 0,
        text_box Flow, X, Y, W, H



// ============================================================================
// 
//    Bullet formatting
// 
// ============================================================================

theme_bullet_size "default", Master:text, 1 -> 62
theme_bullet_size "default", Master:text, 2 -> 52
theme_bullet_size "default", Master:text, Level:integer -> 42
// ----------------------------------------------------------------------------
//    Default size of a bullet
// ----------------------------------------------------------------------------


theme_bullet_color "default:default", Master:text, 1 ->
// ----------------------------------------------------------------------------
//    Default bullet fill color for first level
// ----------------------------------------------------------------------------
    theme_item_color "default:primary"


theme_bullet_color "default:default", Master:text, Level:integer ->
// ----------------------------------------------------------------------------
//    Default bullet fill color for all levels
// ----------------------------------------------------------------------------
    theme_item_color "default:secondary"


theme_bullet_style "default", Master:text, Level:integer ->
// ----------------------------------------------------------------------------
//   Default style of a bullet point
// ----------------------------------------------------------------------------
    plain
    paragraph_space 30, 30
    font_size theme_bullet_size (theme, Master, Level)
    theme_item_color "default:text"


theme_bullet_picture "default", Master:text, Level:integer ->
// ----------------------------------------------------------------------------
//    Default style of a bullet
// ----------------------------------------------------------------------------
    theme_bullet_color "default:default", Master, Level
    theme_bullet_draw S:real -> circle -50% * S, 30% * S, 12% * S
    theme_bullet_draw theme_bullet_size (theme, Master, Level)
