// ****************************************************************************
//  keyboard.xl                                                     Tao project
// ****************************************************************************
//
//   File Description:
//
//     Define a keyboard theme for slides.
//
//
//
//
//
//
//
//
// ****************************************************************************
// Copyright Taodyne SAS, 2012
// Contributors:
//   Baptiste Soulisse <baptiste.soulisse@taodyne.com>
//
// <contact@taodyne.com>
//
// This software is a computer program whose purpose is to serve as a
// document template for the Tao Presentations application.
//
// This software is governed by the CeCILL license under French law and
// abiding by the rules of distribution of free software.  You can  use,
// modify and/ or redistribute the software under the terms of the CeCILL
// license as circulated by CEA, CNRS and INRIA at the following URL
// "http://www.cecill.info".
//
// As a counterpart to the access to the source code and  rights to copy,
// modify and redistribute granted by the license, users are provided only
// with a limited warranty  and the software's author,  the holder of the
// economic rights,  and the successive licensors  have only  limited
// liability.
//
// In this respect, the user's attention is drawn to the risks associated
// with loading,  using,  modifying and/or developing or reproducing the
// software by the user in light of its specific status of free software,
// that may mean  that it is complicated to manipulate,  and  that  also
// therefore means  that it is reserved for developers  and  experienced
// professionals having in-depth computer knowledge. Users are therefore
// encouraged to load and test the software's suitability as regards their
// requirements in conditions enabling the security of their systems and/or
// data to be ensured and,  more generally, to use and operate it in the
// same conditions as regards security.
//
// The fact that you are presently reading this means that you have had
// knowledge of the CeCILL license and that you accept its terms.
// ****************************************************************************

// Aliases for this theme name
theme "Keyboard" -> theme "keyboard"



// ============================================================================
// 
//    Color palette
// 
// ============================================================================

// A few colors we selected for the theme (these are from the Taodyne palette)
theme_color "keyboard", "red"       -> "#AA0022"
theme_color "keyboard", "orange"    -> "#DD3311"
theme_color "keyboard", "yellow"    -> "#EEAA11"
theme_color "keyboard", "green"     -> "#88BB00"
theme_color "keyboard", "ocean"     -> "#229988"
theme_color "keyboard", "sky"       -> "#3399FF"
theme_color "keyboard", "navy"      -> "#003366"
theme_color "keyboard", "purple"    -> "#CC0099"
theme_color "keyboard", "grey"      -> "#555"
theme_color "keyboard", "darkgrey"  -> "#333"
theme_color "keyboard", "black"     -> "black"
theme_color "keyboard", "lightblue" -> "#3399FF"
theme_color "keyboard", "darkblue"  -> "#003366"
theme_color "keyboard", "darkgreen" -> "#006633"
theme_color "keyboard", "brown"     -> "#7E3830"
theme_color "keyboard", Color:text  -> Color



// ============================================================================
// 
//    Styles for the various elements
// 
// ============================================================================

theme_style "keyboard", "title" ->
// ----------------------------------------------------------------------------
//    Style for title in normal slides
// ----------------------------------------------------------------------------
    font "Ubuntu", "Arial", 80, bold
    color theme_color (theme, "darkgrey")
    align 0.5
    vertical_align 0.5


theme_style "keyboard", "subtitle" ->
// ----------------------------------------------------------------------------
//    Style for section subtitle
// ----------------------------------------------------------------------------
    font "Ubuntu", "Arial", 60, bold
    color theme_color (theme, "grey")
    align 0.5
    vertical_align 0.5


theme_style "keyboard", "story" ->
// ----------------------------------------------------------------------------
//    Style for story text
// ----------------------------------------------------------------------------
    font "Ubuntu", "Arial", 50
    color theme_color (theme, "black")
    paragraph_space 30, 30
    align 0
    vertical_align 0.3


theme_style "keyboard", "main:title" ->
// ----------------------------------------------------------------------------
//   Style for main title
// ----------------------------------------------------------------------------
    color theme_color (theme, "red")
    font "Ubuntu", "Arial", 120, bold
    align 0.5
    vertical_align 0.5


theme_style "keyboard", "main:subtitle" ->
// ----------------------------------------------------------------------------
//   Style for main subtitle
// ----------------------------------------------------------------------------
    color theme_color (theme, "navy")
    font "Ubuntu", "Arial", 90, bold
    align 0.5
    vertical_align 0.5


theme_style "keyboard", "section:title" ->
// ----------------------------------------------------------------------------
//    Style for section title
// ----------------------------------------------------------------------------
    font "Ubuntu", "Arial", 120, bold
    color theme_color (theme, "red")
    align 0.5
    vertical_align 0.5


theme_style "keyboard", "section:subtitle" ->
// ----------------------------------------------------------------------------
//    Style for section subtitle
// ----------------------------------------------------------------------------
    font "Ubuntu", "Arial", 85, bold
    color theme_color (theme, "orange")
    align 0.5
    vertical_align 0.5


theme_style "keyboard", "picture" ->
// ----------------------------------------------------------------------------
//   Style for pictures
// ----------------------------------------------------------------------------
    color "white"


theme_style "keyboard", "border" ->
// ----------------------------------------------------------------------------
//   Border color for title pages
// ----------------------------------------------------------------------------
    color theme_color (theme, "grey"), 0.3


theme_style "keyboard", "bullet:1" ->
// ----------------------------------------------------------------------------
//   Bullet color
// ----------------------------------------------------------------------------
    color theme_color (theme, "darkgrey")


theme_style "keyboard", "bullet:2" ->
// ----------------------------------------------------------------------------
//   Bullet color
// ----------------------------------------------------------------------------
    color theme_color (theme, "darkgrey")


theme_style "keyboard", "bullet:3" ->
// ----------------------------------------------------------------------------
//   Bullet color
// ----------------------------------------------------------------------------
    color theme_color (theme, "darkgrey")


theme_style "keyboard", "bullet" ->
// ----------------------------------------------------------------------------
//   Bullet color
// ----------------------------------------------------------------------------
    color theme_color (theme, "darkgrey")


theme_style "keyboard", "box" ->
// ----------------------------------------------------------------------------
//   Style for text boxes
// ----------------------------------------------------------------------------
    color "white", 0.5
    line_color "transparent"



// ============================================================================
// 
//    Background pictures
// 
// ============================================================================

theme_background "keyboard", Master:text ->
// ----------------------------------------------------------------------------
//    Keyboard title background has rectangles at top and bottom
// ----------------------------------------------------------------------------
    clear_color 1, 1, 1, 1
    background -500,
        theme_style theme, "picture"
        image 0, 0, 1.1, 1.1, "image:keyboard.jpg"



// ============================================================================
// 
//    Layout for the various kind of pages
// 
// ============================================================================

theme_slide_width  "keyboard" -> 1920
theme_slide_height "keyboard" -> 1080

theme_layout "keyboard", "main" ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow 0, 0, 1800, 300, "title"
    flow 0, -300, 1800, 200, "subtitle"


theme_layout "keyboard", "section" ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow 0, 0, 1800, 300, "title"
    flow 0, -300, 1800, 200, "subtitle"


theme_layout "keyboard", Master:text ->
// ----------------------------------------------------------------------------
//    Centered flows
// ----------------------------------------------------------------------------
    flow 0, 450, 1800, 140, "title"

    flow 0, -90, 1800, 850, "story"
    flow 0, -90, 1800, 850, "picture"
    flow -475, -90, 850, 850, "left"
    flow  475, -90, 850, 850, "right"



// ============================================================================
// 
//   Titles and subtitles
// 
// ============================================================================

theme_title "keyboard", "main", Body ->
// ----------------------------------------------------------------------------
//    Keyboard main title (centered on the slide)
// ----------------------------------------------------------------------------
    text_flow "title",
        theme_style theme, "main:title"
        Body


theme_subtitle "keyboard", "main", Body ->
// ----------------------------------------------------------------------------
//    Keyboard main title (centered on the slide)
// ----------------------------------------------------------------------------
    text_flow "subtitle",
        theme_style theme, "main:subtitle"
        Body


theme_title "keyboard", "section", Body ->
// ----------------------------------------------------------------------------
//    Keyboard section title (centered on the slide)
// ----------------------------------------------------------------------------
    text_flow "title",
        theme_style theme, "section:title"
        Body


theme_subtitle "keyboard", "section", Body ->
// ----------------------------------------------------------------------------
//    Keyboard section title (centered on the slide)
// ----------------------------------------------------------------------------
    text_flow "subtitle",
        theme_style theme, "section:subtitle"
        Body


theme_title "keyboard", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Keyboard title is at the top
// ----------------------------------------------------------------------------
    text_flow "title",
        theme_style theme, "title"
        Body


theme_subtitle "keyboard", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Keyboard subtitle is below main title
// ----------------------------------------------------------------------------
    text_flow "title",
        theme_style theme, "subtitle"
        Body



// ============================================================================
// 
//   Contents
// 
// ============================================================================

theme_story "keyboard", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Keyboard story box
// ----------------------------------------------------------------------------
    theme_style theme, "story"
    text_flow "story", Body


theme_left_column "keyboard", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Keyboard left column
// ----------------------------------------------------------------------------
    theme_style theme, "story"
    text_flow "left", Body


theme_right_column "keyboard", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Keyboard right column
// ----------------------------------------------------------------------------
    theme_style theme, "story"
    text_flow "right", Body


theme_picture "keyboard", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Keyboard picture box
// ----------------------------------------------------------------------------
    contents 0,
        translatey -90
        theme_style theme, "picture"
        Body


theme_left_column_picture "keyboard", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Keyboard left column picture
// ----------------------------------------------------------------------------
    contents 0,
        translate -475, -90, 0
        theme_style theme, "picture"
        Body


theme_right_column_picture "keyboard", Master:text, Body ->
// ----------------------------------------------------------------------------
//    Keyboard right column picture
// ----------------------------------------------------------------------------
    contents 0,
        translate 475, -90, 0
        theme_style theme, "picture"
        Body



// ============================================================================
// 
//   Text boxes
// 
// ============================================================================

theme_bbox "keyboard", Mast:text, X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//   Draw the background box for text and flows
// ----------------------------------------------------------------------------
    contents -10,
        theme_style theme, "box"
        rounded_rectangle X, Y, W + 10, H + 10, 20


theme_box "keyboard", Master:text, X:real, Y:real, W:real, H:real, Body ->
// ----------------------------------------------------------------------------
//   Draw a keyboard text box
// ----------------------------------------------------------------------------
    theme_bbox theme, Master, X, Y, W, H
    contents 0,
        text_box X, Y, W, H, Body


theme_flow "keyboard", Master:text, X:real, Y:real, W:real, H:real, Flow:text ->
// ----------------------------------------------------------------------------
//   Draw a keyboard text box containing a flow
// ----------------------------------------------------------------------------
    theme_bbox theme, Master, X, Y, W, H
    contents 0,
        text_box Flow, X, Y, W, H



// ============================================================================
// 
//    Bullet formatting
// 
// ============================================================================

theme_bullet "keyboard", Master:text, Level:integer, Text:text ->
// ----------------------------------------------------------------------------
//   Keyboard formatting for a bullet
// ----------------------------------------------------------------------------
    theme_bullet_style theme, Master, Level
    paragraph_break
    anchor
        theme_bullet_color theme, Master, Level
        theme_bullet_picture theme, Master, Level
    text Text


theme_item "keyboard", Master:text, Level:integer, Text:text ->
// ----------------------------------------------------------------------------
//   Keyboard formatting for a text item (without bullet)
// ----------------------------------------------------------------------------
    theme_bullet_style theme, Master, Level
    paragraph_break
    text Text


theme_numbered_item "keyboard", Master:text, Level:integer, Text:text ->
// ----------------------------------------------------------------------------
//   Keyboard formatting for a numbered text item
// ----------------------------------------------------------------------------
    theme_bullet_style theme, Master, Level
    paragraph_break
    keyboard_item_level := Level
    text text keyboard_item_number[keyboard_item_level] & ". "
    text Text
    keyboard_item_number[keyboard_item_level] := 1 + keyboard_item_number[keyboard_item_level]
    keyboard_item_number[keyboard_item_level+1] := 1


theme_reset "keyboard" ->
// ----------------------------------------------------------------------------
//    Reset theme numbers
// ----------------------------------------------------------------------------
    keyboard_item_number[1] := 1
    keyboard_item_number[2] := 1
    keyboard_item_number[3] := 1
    keyboard_item_number[4] := 1
    keyboard_item_number[5] := 1

keyboard_item_level -> 1
data keyboard_item_number


theme_bullet_size "keyboard", Master:text, 1 -> 62
theme_bullet_size "keyboard", Master:text, 2 -> 52
theme_bullet_size "keyboard", Master:text, Level:integer -> 42
// ----------------------------------------------------------------------------
//    Keyboard size of a bullet
// ----------------------------------------------------------------------------


theme_bullet_color "keyboard", Master:text, 1 -> theme_style theme, "bullet:1"
theme_bullet_color "keyboard", Master:text, 2 -> theme_style theme, "bullet:2"
theme_bullet_color "keyboard", Master:text, 3 -> theme_style theme, "bullet:3"
theme_bullet_color "keyboard", Master:text, N -> theme_style theme, "bullet"
// ----------------------------------------------------------------------------
//    Keyboard bullet fill colors
// ----------------------------------------------------------------------------


theme_bullet_style "keyboard", Master:text, Level:integer ->
// ----------------------------------------------------------------------------
//   Keyboard style of a bullet point
// ----------------------------------------------------------------------------
    plain
    theme_style theme, "story"
    margins Level * theme_bullet_size(theme, Master, Level), 0
    font_size theme_bullet_size (theme, Master, Level)


theme_bullet_picture "keyboard", Master:text, Level:integer ->
// ----------------------------------------------------------------------------
//    Keyboard style of a bullet
// ----------------------------------------------------------------------------
    theme_bullet_circle S:real -> circle -50% * S, 30% * S, 12% * S
    theme_bullet_circle theme_bullet_size (theme, Master, Level)

