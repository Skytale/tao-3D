// Slides module definition
//
// Copyright 2010-2011 Taodyne

module_description
    id "70C61E75-4CE2-4020-9E76-797763482B56"
    name "Slides"
    description "Commands and themes for slides: " &
                "definitions that can be used to write presentations. " &
                "Several graphical themes are defined, and can be further " &
                "customized (colors, background, layout)."
    import_name "Slides"
    author "Taodyne SAS"
    website "http://www.taodyne.com"
    version 1.010

module_description "fr",
    name "Thèmes de présentations"
    description "Commandes et thèmes pour créer des présentations " &
                "traditionnelles. " &
                "Plusieurs thèmes graphiques sont fournis, et peuvent " &
                "être personnalisés (couleurs, image de fond, mise en page)."

syntax
    PREFIX     370     !*
    PREFIX     370     !-
    PREFIX     370     !+

// Adding this module's images/ directory to the image: search path
// for the current document
add_search_path "image:", module_dir & "/images"


// ============================================================================
//
//   Theme control variables
//
// ============================================================================

// Logical dimensions for the slide. You can put anything you want here,
// but the aspect ratio should match that of the background picture and be
// close to the aspect ratio of the screens you are going to use.
// This will be used to define the logical coordinates of anything that
// follows "scale_contents".
// Here, we use the size of our background picture
slide_width  -> 1920
slide_height -> 1280


theme -> "default"
theme T:text ->
// ----------------------------------------------------------------------------
//   Select a theme name
// ----------------------------------------------------------------------------
    theme := T


// ============================================================================
//
//    Default theme
//
// ============================================================================

// A few colors we selected for the theme (these are from the Taodyne palette)
theme_color "red"       -> "#AA0022"
theme_color "orange"    -> "#DD3311"
theme_color "yellow"    -> "#EEAA11"
theme_color "green"     -> "#88BB00"
theme_color "ocean"     -> "#229988"
theme_color "sky"       -> "#3399FF"
theme_color "navy"      -> "#003366"
theme_color "purple"    -> "#CC0099"
theme_color "grey"      -> "#555"
theme_color "lightblue" -> "#3399FF"
theme_color "darkblue"  -> "#003366"
theme_color "darkgreen" -> "#006633"
theme_color "blue1"    -> "#D0EAF0"
theme_color "blue2"    -> "#8FB4CB"
theme_color "blue3"    -> "#7AA9C1"
theme_color "blue4"    -> "#1D456B"

background_picture -> "image:seyes.jpg"


theme_background "default" ->
// ----------------------------------------------------------------------------
//    Default background is solid white
// ----------------------------------------------------------------------------
    clear_color 1, 1, 1, 1
    background -6000,
        color "white"
        image 0, 0, 200%, 160%, background_picture


theme_main_title "default", Body ->
// ----------------------------------------------------------------------------
//    Default main title (centered on the slide)
// ----------------------------------------------------------------------------
    box 0, 0, 1600, 500, "main_title",
        font "TeX Gyre Adventor", "Arial", 150, bold
        color theme_color "lightblue"
        align 0.5
        vertical_align 0.5
        Body


theme_title "default", Body ->
// ----------------------------------------------------------------------------
//    Default title is at the top
// ----------------------------------------------------------------------------
    box 0, 500, 1600, 150, "title",
        font "TeX Gyre Adventor", "Arial", 80, bold
        color theme_color "lightblue"
        align 0.5
        vertical_align 0.5
        Body


theme_subtitle "default", Body ->
// ----------------------------------------------------------------------------
//    Default title is at the top
// ----------------------------------------------------------------------------
    box 0, -100, 1600, 150, "subtitle",
        font "TeX Gyre Adventor", "Arial", 80, bold
        color theme_color "lightblue"
        align 0.5
        vertical_align 0.5
        Body


theme_picture "default", Body ->
// ----------------------------------------------------------------------------
//    Default picture box
// ----------------------------------------------------------------------------
    theme_box "default", 0, -150, 1600, 800, "picture"
    do Body


theme_story "default", Body ->
// ----------------------------------------------------------------------------
//    Default story box
// ----------------------------------------------------------------------------
    box 0, -150, 1600, 800, "story",
        font "TeX Gyre Adventor", "Arial", 80, bold
        Body


theme_left_column "default", Body ->
// ----------------------------------------------------------------------------
//    Default left column
// ----------------------------------------------------------------------------
    box -450, -100, 700, 900, "left_column",
        font "TeX Gyre Adventor", "Arial", 80, bold
        Body


theme_right_column "default", Body ->
// ----------------------------------------------------------------------------
//    Default left column
// ----------------------------------------------------------------------------
    box 450, -100, 700, 900, "right_column",
        font "TeX Gyre Adventor", "Arial", 80, bold
        Body


theme_box "default", X:real, Y:real, W:real, H:real, "subtitle" -> false
theme_box "default", X:real, Y:real, W:real, H:real, Kind:text ->
// ----------------------------------------------------------------------------
//    Default style of title box
// ----------------------------------------------------------------------------
    locally
        color "black", 0.5
        line_width 5
        line_color theme_color "orange"
        rounded_rectangle X, Y, W+5, H+5, 20


theme_bullet_size "default", 1 -> 62
theme_bullet_size "default", 2 -> 52
theme_bullet_size "default", Level:integer -> 42


theme_bullet_color "default", Level:integer ->
// ----------------------------------------------------------------------------
//    Default bullet fill color
// ----------------------------------------------------------------------------
    color "blue"


theme_bullet_style "default", Level:integer ->
// ----------------------------------------------------------------------------
//   Define style of a bullet point
// ----------------------------------------------------------------------------
    plain
    paragraph_space 30, 30
    font_size theme_bullet_size ("default", Level)
    color "blue"


theme_bullet_picture "default", Level:integer ->
// ----------------------------------------------------------------------------
//    Draw a bullet
// ----------------------------------------------------------------------------
    theme_bullet_color "default", Level
    draw S:real -> circle -50% * S, 30% * S, 12% * S
    draw theme_bullet_size ("default", Level)


// ============================================================================
//
//   Slide formats
//
// ============================================================================

blank_slide T:text, Body ->
// ----------------------------------------------------------------------------
//   Create an empty slide
// ----------------------------------------------------------------------------
    page T,
        theme_background theme
        Body


title_slide T:text, Body ->
// ----------------------------------------------------------------------------
//   Create a slide for main title
// ----------------------------------------------------------------------------
    blank_slide T,
        Body
        main_title
            text page_label

title_slide T:text -> title_slide T, false


toptitle_slide T:text, Body ->
// ----------------------------------------------------------------------------
//   Create a slide with just a title at the top
// ----------------------------------------------------------------------------
    blank_slide T,
        Body
        title
            text page_label


picture_slide T:text, Body ->
// ----------------------------------------------------------------------------
//   Create a slide for pictures, photos, animations
// ----------------------------------------------------------------------------
    toptitle_slide T,
        picture
            translatey -150
            Body


slide T:text, Body ->
// ----------------------------------------------------------------------------
//   Create a traditional slide with bullet points
// ----------------------------------------------------------------------------
    toptitle_slide T,
        story
            translatey -150
            Body


// ============================================================================
//
//    Slide structure
//
// ============================================================================

background Depth:real, Body ->
// ----------------------------------------------------------------------------
//   Draw something in the background. Backgrounds always fill the screen.
// ----------------------------------------------------------------------------
    locally
        scale_background
        ztrans_background Depth
        Body


contents Depth:real, Body ->
// ----------------------------------------------------------------------------
//   Draw contents, i.e. always fit in the screen
// ----------------------------------------------------------------------------
    locally
        scale_contents
        ztrans_contents Depth
        Body


main_title Body ->
// ----------------------------------------------------------------------------
//   Draw a main title
// ----------------------------------------------------------------------------
    contents 0,
        theme_main_title theme, Body


title Body ->
// ----------------------------------------------------------------------------
//   Draw a title
// ----------------------------------------------------------------------------
    contents 0,
        theme_title theme, Body


subtitle Body ->
// ----------------------------------------------------------------------------
//   Draw a subtitle
// ----------------------------------------------------------------------------
    contents 0,
        theme_subtitle theme, Body


picture Body ->
// ----------------------------------------------------------------------------
//   Box for picture, photos, animations
// ----------------------------------------------------------------------------
    contents 0,
        theme_picture theme, Body


story Body ->
// ----------------------------------------------------------------------------
//   Draw something as the story, i.e. a rectangle somewhere in the middle
// ----------------------------------------------------------------------------
    contents 0,
        theme_story theme, Body


left_column Body ->
// ----------------------------------------------------------------------------
//   Draw something as the left side of the story
// ----------------------------------------------------------------------------
    contents 0,
        theme_left_column theme, Body


right_column Body ->
// ----------------------------------------------------------------------------
//   Draw something as the right side of the story
// ----------------------------------------------------------------------------
    contents 0,
        theme_right_column theme, Body


box X:real, Y:real, W:real, H:real, Kind:text, Body ->
// ----------------------------------------------------------------------------
//   Draw a box for an element kind
// ----------------------------------------------------------------------------
    locally
        locally
            theme_box theme, X, Y, W, H, Kind
        translatez 1 // Use to avoid depth problem
        text_box X, Y, W, H,
            do Body


*T:text -> bullet 1, T
// ----------------------------------------------------------------------------
//   Display a first-level bullet
// ----------------------------------------------------------------------------


-T:text -> bullet 2, T
// ----------------------------------------------------------------------------
//   Display a second-level bullet
// ----------------------------------------------------------------------------


+T:text -> bullet 3, T
// ----------------------------------------------------------------------------
//   Display a third-level bullet
// ----------------------------------------------------------------------------


bullet Level, Text ->
// ----------------------------------------------------------------------------
//   Draw a bullet point with a given text
// ----------------------------------------------------------------------------
    margins Level * theme_bullet_size(theme, Level), 0
    paragraph_break
    anchor
        theme_bullet_picture theme, Level
    theme_bullet_style theme, Level
    text Text


no_bullet Level, Text ->
// ----------------------------------------------------------------------------
//   Draw a given text without bullet point
// ----------------------------------------------------------------------------
    margins (Level - 50% - 12% ) * theme_bullet_size(theme, Level), 0
    paragraph_break
    theme_bullet_style theme, Level
    text Text


!*T:text -> no_bullet 1, T
// ----------------------------------------------------------------------------
//   Display a first-level text without bullet
// ----------------------------------------------------------------------------


!-T:text -> no_bullet 2, T
// ----------------------------------------------------------------------------
//   Display a second-level text without bullet
// ----------------------------------------------------------------------------


!+T:text -> no_bullet 3, T
// ----------------------------------------------------------------------------
//   Display a third-level text without bullet
// ----------------------------------------------------------------------------


// ============================================================================
//
//    Smart rescaling of the contents
//
// ============================================================================

// Two ways to scale contents when resizing the window :
// For background pictures, we'd rather not see some white around them,
// so we scale to crop the picture, using the ma function.
// For contents, on the other hand, we want to keep everything visible, so
// we scale to fit the contents, using the min function.
background_scale -> max(window_width/slide_width, window_height/slide_height)
contents_scale   -> min(window_width/slide_width, window_height/slide_height)
scale_background -> scale3 background_scale
scale_contents   -> scale3 contents_scale
scale3 S:real    -> scale S, S, S

ztrans_contents Z:real ->
// ----------------------------------------------------------------------------
//   Translate a content in Z without a change in apparent dimensions
// ----------------------------------------------------------------------------
    Z0 -> camera_position at 3
    translatez Z/contents_scale
    scale3 (Z0-Z)/Z0


ztrans_background Z:real ->
// ----------------------------------------------------------------------------
//    Translate a backgtround in Z without a change in apparent dimensions
// ----------------------------------------------------------------------------
    Z0 -> camera_position at 3
    translatez Z/background_scale
    scale3 (Z0-Z)/Z0




