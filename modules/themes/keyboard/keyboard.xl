// ****************************************************************************
//  keyboard.xl                                                    Tao project 
// ****************************************************************************
// 
//   File Description:
// 
//    Keyboard theme - A simple theme with a keyboard in background 
// 
// 
// 
// 
// 
// 
// 
// 
// ****************************************************************************
// This software is licensed under the GNU General Public License v3
// See file COPYING for details.
//  (C) 2012-2014 Christophe de Dinechin <christophe@taodyne.com>
//  (C) 2012-2014 Jerome Forissier <jerome@taodyne.com>
//  (C) 2012-2014 Baptiste Soulisse <baptiste.soulisse@taodyne.com>
//  (C) 2012-2014 Catherine Burvelle <catherine@taodyne.com>
//  (C) 2012-2014 Taodyne SAS
// ****************************************************************************

import Slides
import Charts

module_description
    id "0CA6CDE4-8EA2-4A66-B18B-AA117457114D" 
    name "Keyboard theme"
    description "A simple theme with a keyboard in background"
    import_name "KeyboardTheme"
    author "Taodyne SAS"
    website "http://www.taodyne.com"
    version 1.004

module_description "fr",
    name "Thème Clavier"
    description "Un thème simple avec un clavier en fond d'écran"

// Adding this module's images/ directory to the BlueClaire: search path
// for the current document
add_search_path "Keyboard:", module_dir


// ============================================================================
// 
//    Theme definition
// 
// ============================================================================

// Aliases for this theme name
theme "Keyboard" -> theme "keyboard"


// ============================================================================
// 
//    Color palette
// 
// ============================================================================

// A few colors we selected for the theme (these are from the Taodyne palette)
theme_color "keyboard", "darkgrey"  -> "#333"
theme_color "keyboard", "grey"      -> "#555"
theme_color "keyboard", "black"     -> "black"
theme_color "keyboard", "red"       -> "#B46"
theme_color "keyboard", "orange"    -> "#C54"
theme_color "keyboard", "navy"      -> "#456"

theme_color "keyboard", Master:text, "story" -> color "#333"
theme_color "keyboard", Master:text, "title" -> color "#555"
theme_color "keyboard", Master:text, "subtitle" -> color "#777"

theme_font "keyboard", Master:text, "story" -> font "Ubuntu", "Arial"
theme_boxstyle "keyboard",Master:text,Style:text-> color "white", 0.5

theme_box "keyboard",Master:text,"picture", X:real,Y:real,W:real,H:real -> nil
theme_box "keyboard",Master:text,"leftpic", X:real,Y:real,W:real,H:real -> nil
theme_box "keyboard",Master:text,"rightpic", X:real,Y:real,W:real,H:real -> nil

theme_box "keyboard",Master:text,Style:text, X:real,Y:real,W:real,H:real ->
// ----------------------------------------------------------------------------
//   Draw the background box for text and flows
// ----------------------------------------------------------------------------
    contents -10,
        theme_boxstyle theme, Master, Style
        rounded_rectangle X, Y, W + 10, H + 10, 20



// ============================================================================
// 
//    Background picture
// 
// ============================================================================

theme_background "keyboard", Master:text ->
// ----------------------------------------------------------------------------
//    Keyboard title background has rectangles at top and bottom
// ----------------------------------------------------------------------------
    clear_color 1, 1, 1, 1
    background -500,
        color "white"
        image 0, 0, 1.1, 1.1, "Keyboard:keyboard.jpg"



// ============================================================================
// 
//   Text boxes
// 
// ============================================================================

theme_bbox "keyboard", Master:text, X:real, Y:real, W:real, H:real ->
// ----------------------------------------------------------------------------
//   Draw the background box for text and flows
// ----------------------------------------------------------------------------
    contents -10,
        theme_style theme, "box"
        rounded_rectangle X, Y, W + 10, H + 10, 20
