theme "LuckyStars"
import VLCAudioVideo
import MovieCredits
import Animate


main_title_slide "Main title",
    title
        text "Lucky Star theme"
    subtitle
        text "A space-based theme"


section_slide "Section title",
    title
        text "Lucky Star theme"
    subtitle
        text "A simple space-based theme"


slide "Bullet points",
    * "Bullet points"
    ** "More bullet points"
    *** "Deeper"
    --
    * "Again"
    ++ "Numbered text"
    ++ "Numbered"
    -- "Additional text"


picture_slide "Picture slide",
    mc_size 1200, 2000

    translatey -100
    mc_star_wars_crawl 55,
        title T:text -> align 0.5; text T
        line T:text -> paragraph_break; align 0, 1; text T

        title "A NEW HOPE FOR PRESENTATIONS"
        line "The world of presentations is in chaos. The ancient power of bullet points is gone. Kids born with iPads in their hands don't believe in this old religion anymore."
        line "But a glimmer of hope remains. Hidden in a remote place called Taodyne, three computer gurus versed in ancient computer science found a new way to jolt your audience."
        line "The fight has just begun, a fight for the freedom to put some creativity back in presentations. To win this battle, Taodyne needs your help..."

    movie_only ""


picture_slide "YouTube Video slide",
    left_picture
        translate 100, 100, -4500 + 4100 * fade_in(page_time, 50)
        rotatey 35
        scale3 fade_in(page_time, 50)
        movie 0, 0, 3.5, 3.5, "http://www.youtube.com/watch?v=oALxLNOhI6I&fmt=43"
    right_column
        - "Video on the left:"
        - "2010 (1984)"


picture_slide "Chart slide",
    chart_current "My chart"
    once
        for i in 1..4 loop
            chart_push_data 1, 200 + 50 * i
            chart_push_data 2, 150 - 20 * i
            chart_push_data 3, 165 + i * 15 * (-1) ^ i
        chart_set_legend 1, "First set"
        chart_set_legend 2, "Second set"
        chart_set_legend 3, "Third set"
        chart_set_title "Sample chart"
        chart_set_format "3D"
        chart_set_type "bar"

    light 0
    light_attenuation 1 / scaling_factor, 0, 0
    light_position 0, 0, 3000, 1
    material_ambient 1.0, 1.0, 1.0, 1.0
    material_diffuse 1.0, 1.0, 1.0, 1.0

    chart 1..3
