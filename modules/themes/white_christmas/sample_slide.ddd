import VLCAudioVideo
import WhiteChristmasTheme

theme "WhiteChristmas"

main_title_slide "Main title",
    title
        text "White Christmas theme"
    subtitle
        text "A white winter theme"


section_slide "Section title",
    title
        text "Section title"
    subtitle
        text "Section subtitle"


slide "Bullet points",
    * "Bullet points"
    ** "More bullet points"
    *** "Deeper"
    --
    * "Again"
    ++ "Numbered text"
    ++ "Numbered"
    -- "Additional text"


picture_slide "Picture slide",
    rotatez 2
    color "white"
    texture "http://jags-webdesign.com/wp-content/uploads/2013/12/white-christmas-wallpaperwhite-christmas-hd-wallpapers-inn-u459ifwx.jpg"
    fuzzy_border 0.1
    // circle_fuzzy_border 0, 0, 100%, 10%
    rounded_rectangle 0, 0, 0.5*texture_width, 0.5*texture_height, 40
    movie_only ""


title_slide "YouTube Video slide",
    left_picture
        translate 0, 0, -150
        rotatez 5
        movie_texture "http://www.youtube.com/watch?v=igaOTqgXbnA"
        star 0, 0, 1.6 * texture_width, 2.0 * texture_height, 25, 85%
        star -180, 300, 0.8 * texture_width, 0.8 * texture_height, 5, 40%
        rotate_z -5
        star 150, 250, 0.6 * texture_width, 0.6 * texture_height, 7, 60%
        star 50, -290, 0.7 * texture_width, 0.8 * texture_height, 5, 40%
    right_column
        - "Video on the left:"
        -- { italic """White Christmas"""; text " by Bing Crosby and Frank Sinatra" }


picture_slide "Chart slide",
    movie_only ""
    once
        chart_current "My chart"
        chart_reset
        for i in 1..7 loop
            chart_push_data 1, 200 + 50 * sin(132.3*i)
            chart_push_data 2, 150 - 20 * exp(0.33*i) * sin(27*i)
            chart_push_data 3, 165 + i * 15 * (-1.05) ^ i
        chart_set_legend 1, "Astronauts"
        chart_set_legend 2, "Martians"
        chart_set_legend 3, "Art"
        chart_set_legend 4, "Zero"
        chart_set_legend 5, "Infinity"
        chart_set_legend 6, "Numbers"
        chart_set_legend 7, "Gravity"
        chart_set_title "Sample chart"
        chart_set_format "3D"
        chart_set_type "bar"

    chart_set_type my_type (seconds / 4 mod 4)
    my_type 0 -> "bar"
    my_type 1 -> "line"
    my_type 2 -> "pie"
    my_type 3 -> "area"
    chart 1..3
