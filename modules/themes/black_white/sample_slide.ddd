import VLCAudioVideo


main_title_slide "Main title",
    theme "BlackOnWhite"
    title
        text "Basic themes"
    subtitle
        text "Neutral and non-intrusive themes"


section_slide "Section title",
    theme "WhiteOnBlack"
    title
        text "Section title"
    subtitle
        text "Section subtitle"


slide "Bullet points",
    theme "WhiteOnGray"
    * "Bullet points"
    ** "More bullet points"
    *** "Deeper"
    --
    * "Again"
    ++ "Numbered text"
    ++ "Numbered"
    -- "Additional text"


picture_slide "Picture slide",
    theme "BlackOnGray"
    translatez -500
    rotatez 3 * sin (1.2*page_time)^25 * sin(18*page_time)
    image 0, 0, 0.9, 0.9, "http://images2.fanpop.com/image/photos/14400000/City-Lights-charlie-chaplin-14440701-1600-1213.jpg"
    movie_only ""


picture_slide "YouTube Video slide",
    theme "WhiteOnBlack"
    left_picture
        translate 100, 100, -150
        rotatez 5
        movie 0, 0, 1, 1, "http://www.youtube.com/watch?v=OK7pfLlsUQM&fmt=45"
    right_column
        - "Video on the left: "
        - "The Artist (2011)"


picture_slide "Chart slide",
    theme "BlackOnWhite"
    chart_init -> 0
    if(chart_init = 0) then
        chart_current "My chart"
        chart_reset
        for i in 1..4 loop
            chart_push_data 1, 200 + 50 * i
            chart_push_data 2, 150 - 20 * i
            chart_push_data 3, 165 + i * 15 * (-1) ^ i
        chart_init := 1

    chart_set_legend 1, "First set"
    chart_set_legend 2, "Second set"
    chart_set_legend 3, "Third set"

    chart_set_title "Sample chart"
    chart_set_format "3D"

    light 0
    material_ambient 1.0, 1.0, 1.0, 1.0
    material_diffuse 1.0, 1.0, 1.0, 1.0

    chart "My chart", "bar"

