theme "PastelTriangles"
import VLCAudioVideo


main_title_slide "Main title",
    title
        text "Pastel Triangles theme"
    subtitle
        text "A theme with automated geometric backgrounds"


section_slide "Section title",
    title
        text "Section title"
    subtitle
        text "Section subtitle"


slide "Bullet points",
    * "Bullet points"
    ** "More bullet points"
    *** "Deeper"
    --
    * "Again"
    ++ "Numbered text"
    ++ "Numbered"
    -- "Additional text"


picture_slide "Picture slide",
    locally
        if seconds = 58 then
            rotatez -360 * page_time
        translate 0, 0, -500
        draw_triangle with 0..seconds
    locally
        if seconds = 59 then
            rotatez 360 * page_time
        translate 20, 20, -200
        scale 0.6, 0.6, 0.6
        draw_triangle with 0..minutes
        show 0.1
        draw_triangle with minutes..59
    locally
        if seconds = 0 then
            rotatez -360 * page_time
        translate -20, -5, -50
        rotatez -30 * hours
        color "black", 0.4
        triangle 0, 100, 150, 150
    
    draw_triangle N:integer ->
        rotatez -6
        color_hsv 6 * N, 0.3, 0.9, 0.9
        triangle 0, 500, 50, 150
    movie_only ""


picture_slide "YouTube Video slide",
    left_picture
        translate 0, 150 + 300 * sin (0.5*time) ^ 140, -150
        rotatez 5
        movie 0, 0, 1.5, 1.5, "http://www.youtube.com/watch?v=30T5RQv6rxw&fmt=43"
    right_column
        - "Video on the left:"
        - "Alpha Waves, the first 3D platform game on a PC, edited in 1990 by Infogrames"


picture_slide "Chart slide",
    chart_init -> 0
    if(chart_init = 0) then
        chart_current "My chart"
        chart_reset
        for i in 1..4 loop
            chart_push_data 1, 200 + 50 * i
            chart_push_data 2, 150 - 20 * i
            chart_push_data 3, 165 + i * 15 * (-1) ^ i
        chart_init := 1

    chart_set_legend 1, "First set"
    chart_set_legend 2, "Second set"
    chart_set_legend 3, "Third set"

    chart_set_title "Sample chart"
    chart_set_format "3D"

    light 0
    material_ambient 1.0, 1.0, 1.0, 1.0
    material_diffuse 1.0, 1.0, 1.0, 1.0

    chart "My chart", "bar"
