load "style.xl" 
load "clock.xl" 
show_background 
show_title 
show_page_info 
page "Welcome to Tao", do 
    show_source false 
    story 
        vertical_center 0.2 
        vertical_spacing 100% 
        * "Tao is a new way to share ideas and tell stories" 
        -"Focus on ideas, not just presentation" 
        -"Give more life to your stories with advanced animations" 
        -"Collaborate on complex ideas, keep track of their history" 
        * "This tutorial will introduce you to Tao"
    
    story 
        vertical_center 0.9 
        request "Hit the 'Page Down' key to continue"
    

page "Why call it Tao (ÈÅì)?", do 
    story 
        vertical_center 0.0 
        * "Here is what Wikipedia has to say:"
    
    story 
        vertical_center 0.95 
        request "The box above is a live web page. Click to activate..."
    
    active_widget 
        reset_transform
        color 95% ,100% ,97% ,70% 
        url 0 * window_scale, -40 * window_scale, 800 * window_scale, 350 * window_scale, constant "http://en.wikipedia.org/wiki/Tao#Meanings"
    
    active_widget 
        reset_transform

        color 1, 1, 1, 1 
        push_button 300 * window_scale, -280 * window_scale, 160, 25, "MoreURLs", "More Web Quotes...", goto_page "More Web Pages"
        
    

page "Hello, World!", do 
    story 
        vertical_center 0.0 
        * "Here is Tao's version of the venerable 'Hello World'"
    
    story 
        vertical_center 1.0 
        request <<A presentation like this is very simple to do. Hit the '?' key to know more...
        Otherwise , hit 'Page Down'>>
    
    active_widget 
        color 1, 1, 1, 1 
        push_button 340, -190, 120, 25, "ViewSource", "Learn more...", goto_page "Hello World Source"
    
    page_link "?", "Hello World Source" 
    page_link "PageDown", "3D Graphics" 
    locally 
        frame_texture 1024, 768, do 
            load "hello.ddd"
        
        shape 
            color 1, 1, 1, 1 
            rounded_rectangle 0, -40, 512, 384, 40
        
    

page "Hello World Source", do 
    story 
        vertical_center 0.0 
        * "Tao documents are simple programs"
    
    locally 
        vertical_center 0.0 
        font "Courier New" 
        font_size 13.0 
        bold 
        text_box 0, -90, 620, 440, do 
            center 0.0 
            color 0% ,40% ,20% ,100% 
            text load_text "hello.ddd"
        
        text_box 40, -90, 620, 440, do 
            center 1.0 
            color 80% ,20% ,20% ,100% 
            text load_text "hello.txt"
        
    

page "3D Graphics", do 
    page_link "PageUp", "Hello, World!" 
    frame_texture 1024, 768, do 
        load "cubes.ddd"
    
    shape 
        color 80% ,80% ,80% ,60% 
        rounded_rectangle 91, -47, 640, 480, 79
    
    texture ""
    story 
        vertical_center 0.0 
        * "Tao uses advanced 3D graphics technology" 
        -"All documents can use 3D easily" 
        -"Wow your audience" 
        -"Present data in new and creative ways"
    

MyData -> data 
    "Quarter", "Sales", "Revenues", "Schtroumpfs" 
    "Q1", 10, -40, 30.3 + 20 * seconds mod 37 
    "Q2", 20, -20, 60.4 + 40 * cos seconds 
    "Q3", 40, 30, 75.25 + 40 * sin (seconds * 0.01) 
    "Q4", 60, 30, 10.54

ShowTableCell X:text -> 
    table_cell 
        center 0.5 
        color 0% ,0% ,20% ,1 
        text X
    

ShowTableCell X:integer -> 
    table_cell 
        center 1.0 
        if X >= 0 then 
            color 0% ,70% ,0% ,1
        else 
            color 70% ,0, 0, 1
        
        text text X
    

ShowTableCell X:real -> 
    table_cell 
        center 1.0 
        color 0% ,0, X / 100.0, 1 
        text text X
    

page "Tables and Data", do 
    story 
        vertical_center 0.0 
        * "Tao makes it easy to manipulate tables and data" 
        font "Arial" 
        font_size 22 
        paragraph_break 
        center 0.5 
        table 5, 4, do 
            table_cell_fill 
                color real table_cell_row / table_rows, real table_cell_column / table_columns, 0, 30% 
                line_color 0, 0, 1, 80% 
                line_width 1 
                rectangle table_cell_x, table_cell_y, table_cell_w, table_cell_h
            
            table_cell_margins 5, 5 
            ShowTableCell with MyData
        
        paragraph_break 
        center 0.0 
        * "Tables integrate very naturally with other items"
    

page "More Web Pages", do 
    story 
        vertical_center 0.0 
        * "PowerPoint makes us stupid!" 
        line_break 
        italic 
        font_size 16 
        text "Gen. James N. Mattis, US Joint Forces Commander, Marine Corps"
    
    active_widget 
        reset_transform
        color 95% ,100% ,97% ,70% 
        url 0 * window_scale, -70 * window_scale, 800 * window_scale, 400 * window_scale, constant "http://www.nytimes.com/2010/04/27/world/27powerpoint.html"
    
    active_widget 
        reset_transform 
        color 1, 1, 1, 1 
        push_button 340 * window_scale, -280 * window_scale, 120 * window_scale, 25 * window_scale, "MoreURLs", "More Web...", goto_page "More Quotes"
    
