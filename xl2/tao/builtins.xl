0! -> 1
N! -> N * (N-1)!

// If-then-else statement
if true then TrueBody else FalseBody    -> do TrueBody
if false then TrueBody else FalseBody   -> do FalseBody

if true then TrueBody                   -> do TrueBody
if false then TrueBody                  -> false

// 3D Shapes
sphere x:real, y:real, z:real, r:real   -> sphere x,y,z,2*r,2*r,2*r,25,25
sphere r:real                           -> 
    sphere 0.0, 0.0, 0.0, 2*r, 2*r, 2*r, 25, 25

//2D Shapes
circle x:real, y:real, r:real           -> ellipse x, y, 2*r, 2*r
circle r:real                           -> ellipse 0.0, 0.0, 2*r, 2*r
oval x:real, y:real, w:real, h:real     -> ellipse x, y, w, h
oval w:real, h:real                     -> ellipse 0.0, 0.0, w, h

circular_sector x:real, y:real, r:real, a:real, b:real ->
    ellipse_arc x, y, 2*r, 2*r, a, b
circular_sector r:real, a:real, b:real  -> ellipse_arc 0.0, 0.0, 2*r,2*r, a, b

rectangle w:real, h:real                -> rectangle 0.0, 0.0, w, h

square x:real, y:real, s:real           -> rectangle x, y, s, s 
square s:real                           -> rectangle 0.0, 0.0, s, s 

triangle w:real, h:real                 -> triangle 0.0, 0.0, w, h
right_triangle w:real, h:real           -> right_triangle 0.0, 0.0, w, h

rounded_rectangle x:real, y:real, w:real, h:real, r:real ->
    rounded_rectangle x, y, w, h, r, r
rounded_rectangle w:real, h:real, r:real-> rounded_rectangle 0.0, 0.0, w, h, r,r

star_polygon x:real, y:real, r:real, p:integer, q:integer ->
    star_polygon x, y, 2*r, 2*r, p, q
polygram x:real, y:real, r:real, p:integer, q:integer ->
    star_polygon x, y, 2*r, 2*r, p, q
polygon x:real, y:real, r:real, p:integer ->
    star_polygon x, y, 2*r, 2*r, p, 1

schläfli t, {p:integer/q:integer}       -> star_polygon t, p, q
schläfli t, {p:integer}                 -> polygon t, p

triangle t                              -> polygon t, 3
tetragon t                              -> polygon t, 4
pentagon t                              -> polygon t, 5
hexagon t                               -> polygon t, 6
heptagon t                              -> polygon t, 7
octagon t                               -> polygon t, 8
nonagon t                               -> polygon t, 9
decagon t                               -> polygon t, 10
hendecagon t                            -> polygon t, 11
dodecagon t                             -> polygon t, 12
tridecagon t                            -> polygon t, 13
tetradecagon t                          -> polygon t, 14
pentadecagon t                          -> polygon t, 15
hexadecagon t                           -> polygon t, 16
heptadecagon t                          -> polygon t, 17
octadecagon t                           -> polygon t, 18
enneadecagon t                          -> polygon t, 19
icosagon t                              -> polygon t, 20

pentagram t                             -> polygram t, 5, 2
hexagram t                              -> polygram t, 6, 2
star_of_david t                         -> polygram t, 6, 2
heptagram t                             -> polygram t, 7, 2
star_of_lakshmi t                       -> polygram t, 8, 2
octagram t                              -> polygram t, 8, 3
nonagram t                              -> polygram t, 9, 3
decagram t                              -> polygram t, 10, 3

star x:real, y:real, 0, R:real, p:integer ->
    star x, y, 0, 0, 0, p

star x:real, y:real, r:real, R:real, p:integer ->
    star x, y, 2*r, 2*r, R/r, p

x..y / t -> x + (y-x) * (1 + sin(time/t * 2*pi)) / 2

selection_background_color ->
    color 0.7..1.0 / 5.0, 0.8..1.0 / 9.0, 0.9..1.0 / 6.0, 0.4

selection_outline_color ->
    line_color 0.8, 0.6, 0.3..0.7 / 3.0, 0.8

handle_color ->
    color 0.0..0.4 / 3.0, 0.0, 0.0, 0.6
    line_color 0,0,0,0

draw_selection x,y,z,w,h,d ->
    selection_background_color
    selection_outline_color
    cube x-w/2, y-h/2, z-d/2, w, h, d

draw_3D_selection x,y,z,w,h,d ->
    selection_background_color
    selection_outline_color
    cube x, y, z, w, h, d

draw_widget_selection x,y,w,h ->
    selection_outline_color
    color 0,0,0,0
    rectangle x, y, w, h

draw_selection x,y,w,h ->
    selection_background_color
    selection_outline_color
    rectangle x, y, w+4, h+4

draw_selection_rectangle x, y, w, h ->
    draw_selection x,y,w,h

draw_handle x, y, z ->
    handle_color
    cube x, y, z, 8,8,8

draw_rotation_base x, y, z ->
    selection_outline_color
    selection_background_color
    locally
        rotatex 90
        sphere x, y, z, 50, 50, 50, 16, 8
draw_rotation_tip x, y, z -> draw_handle x, y, z

draw_translation_base x, y, z ->
    selection_outline_color
    selection_background_color
    cube x, y, z, 50, 50, 50
draw_translation_x x, y, z -> draw_handle x + 40, y, z
draw_translation_y x, y, z -> draw_handle x, y + 40, z
draw_translation_z x, y, z -> draw_handle x, y, z + 40

draw_scale_base x, y, z ->
    selection_outline_color
    selection_background_color
    cube x, y, z, 50, 20, 20
    cube x, y, z, 20, 50, 20
    cube x, y, z, 20, 20, 50
draw_scale_x x, y, z -> draw_handle x + 40, y, z
draw_scale_y x, y, z -> draw_handle x, y + 40, z
draw_scale_z x, y, z -> draw_handle x, y, z + 40

theme_colors -> 
    color 0.5, 0.5, 0.5, 1
    line_color 0, 0, 0, 1

menu_insert -> {
menu    "Insert"
submenu   "Shape"
menu_item   "Line", insert { text "Line" }
menu_item   "Line with Arrowhead", insert { text "Line with Arrowhead" }
menu_item   "Line with Two Arrowheads", insert { text "Line with Two Arrowheads" }
menu_item   "Rectangle", insert
    locally
        theme_colors
        rectangle 0, 0, 100, 100
menu_item   "Rounded Rectangle", insert
    locally
        theme_colors
        rounded_rectangle 0, 0, 100, 100, 15
menu_item   "Oval", insert
    locally
        theme_colors
        oval 0, 0, 100, 100
menu_item   "Triangle", insert
    locally
        theme_colors
        triangle 0, 0, 100, 100
menu_item   "Right Triangle", insert
    locally
        theme_colors
        right_triangle 0, 0, 100, 100
menu_item   "Arrow", insert
    locally
        theme_colors
        arrow 0, 0, 100, 100, 56, 34
menu_item   "Double Arrow", insert
    locally
        theme_colors
        double_arrow 0, 0, 100, 100, 44, 34
menu_item   "Diamond", insert
    locally
        theme_colors
        tetragon 0, 0, 50
menu_item   "Quote Bubble", insert { text "Quote Bubble" }
menu_item   "Callout", insert { text "Callout" }
menu_item   "Star", insert 
    locally
        theme_colors
        star 0, 0, 50, 25, 5
menu_item   "Polygon", insert 
    locally
        theme_colors
        polygon 0, 0, 50, 5
}
