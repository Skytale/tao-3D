0! -> 1
N! -> N * (N-1)!

// If-then-else statement
if true then TrueBody else FalseBody -> do TrueBody
if false then TrueBody else FalseBody -> do FalseBody

if true then TrueBody -> do TrueBody
if false then TrueBody -> false


// Graphics stuff
sphere x:real, y:real, z:real, r:real -> 
    sphere x,y,z,r,25,25

sphere r:real -> 
    sphere 0.0, 0.0, 0.0, r

circle r:real ->
    circle 0.0, 0.0, r

circular_sector r:real, a:real, b:real ->
    circular_sector 0.0, 0.0, r, a, b

rectangle w:real, h:real ->
    rectangle 0.0, 0.0, w, h

square t, c:real ->
    rectangle t, c, c 

square c:real ->
    rectangle c, c 

rounded_rectangle w:real, h:real, r:real ->
    rounded_rectangle 0.0, 0.0, w, h, r

regular_star_polygon r:real, p:integer, q:integer ->
    regular_star_polygon 0.0, 0.0, r, p, q

regular_polygram t -> regular_star_polygon t

regular_polygon t ->
    regular_star_polygon t, 1

schläfli t, {p:integer/q:integer} -> regular_star_polygon t, p, q
schläfli t, {p:integer} -> regular_polygon t, p

equilateral_triangle t -> regular_polygon t, 3
regular_tetragon t -> regular_polygon t, 4
regular_pentagon t -> regular_polygon t, 5
regular_hexagon t -> regular_polygon t, 6
regular_heptagon t -> regular_polygon t, 7
regular_octagon t -> regular_polygon t, 8
regular_nonagon t -> regular_polygon t, 9
regular_decagon t -> regular_polygon t, 10
regular_hendecagon t -> regular_polygon t, 11
regular_dodecagon t -> regular_polygon t, 12
regular_tridecagon t -> regular_polygon t, 13
regular_tetradecagon t -> regular_polygon t, 14
regular_pentadecagon t -> regular_polygon t, 15
regular_hexadecagon t -> regular_polygon t, 16
regular_heptadecagon t -> regular_polygon t, 17
regular_octadecagon t -> regular_polygon t, 18
regular_enneadecagon t -> regular_polygon t, 19
regular_icosagon t -> regular_polygon t, 20

regular_pentagram t -> regular_star_polygon t, 5, 2
star t -> regular_star_polygon t, 5, 2
regular_hexagram t -> regular_star_polygon t, 6, 2
star_of_david t -> regular_star_polygon t, 6, 2
regular_heptagram t -> regular_star_polygon t, 7, 2
star_of_lakshmi t -> regular_star_polygon t, 8, 2
regular_octagram t -> regular_star_polygon t, 8, 3
regular_nonagram t -> regular_star_polygon t, 9, 3
regular_decagram t -> regular_star_polygon t, 10, 3

box xl,yl,zl,xu,yu,zu ->
        quad_strip
            vertex xl, yu, zu
            vertex xu, yu, zu
            vertex xl, yl, zu
            vertex xu, yl, zu
            vertex xl, yl, zl
            vertex xu, yl, zl
            vertex xl, yu, zl
            vertex xu, yu, zl
            vertex xl, yu, zu
            vertex xu, yu, zu
        quad_strip
            vertex xu, yu, zu
            vertex xu, yu, zl
            vertex xu, yl, zu
            vertex xu, yl, zl
        quad_strip
            vertex xl, yl, zu
            vertex xl, yl, zl
            vertex xl, yu, zu
            vertex xl, yu, zl

boxedges xl,yl,zl,xu,yu,zu ->
        line_loop
            vertex xl, yu, zu
            vertex xu, yu, zu
            vertex xu, yl, zu
            vertex xl, yl, zu
        line_loop
            vertex xu, yu, zl
            vertex xl, yu, zl
            vertex xl, yl, zl
            vertex xu, yl, zl
        lines
            vertex xu, yu, zu
            vertex xu, yu, zl
            vertex xu, yl, zu
            vertex xu, yl, zl
            vertex xl, yl, zu
            vertex xl, yu, zu
            vertex xl, yu, zl

draw_selection x,y,z,w,h,d ->
    r -> 2.0
    k -> (1+sin(time*2*pi))*0.05+0.9
    color k*0.71,k*0.83,k,0.4
    box x-w/2-r, y-h/2-r, z-d/2-r, x+w/2+r, y+h/2+r, z+d/2+r
    color k*0.6,k*0.70,k*0.85,0.6
    boxedges x-w/2-r, y-h/2-r, z-d/2-r, x+w/2+r, y+h/2+r, z+d/2+r

draw_selection_pyramid x,y,z,w,h,d ->
    polygon
        color 1.0, 1.0, 1.0, 0.1; vertex x,   y,   z+d/2
        color 1.0, 0.0, 0.0, 0.4; vertex x-w/2, y-h/2, z
        color 0.0, 0.0, 1.0, 0.4; vertex x-w/2, y+h/2, z
        color 0.0, 1.0, 1.0, 0.4; vertex x+w/2, y+h/2, z
        color 1.0, 1.0, 0.0, 0.4; vertex x+w/2, y-h/2, z
        color 1.0, 0.0, 0.0, 0.4; vertex x-w/2, y-h/2, z
    polygon
        color 0.0, 0.0, 0.0, 0.4; vertex x,   y,   z-d/2;
        color 1.0, 0.0, 0.0, 0.4; vertex x-w/2, y-h/2, z;
        color 0.0, 0.0, 1.0, 0.4; vertex x-w/2, y+h/2, z;
        color 0.0, 1.0, 1.0, 0.4; vertex x+w/2, y+h/2, z;
        color 1.0, 1.0, 0.0, 0.4; vertex x+w/2, y-h/2, z;
        color 1.0, 0.0, 0.0, 0.4; vertex x-w/2, y-h/2, z;

draw_selection_3D x,y,z,w,h,d ->
    draw_selection x,y,z,w,h,d
    selector "depth"
    regular_decagram x, y, (w+h)/5

draw_selection x,y,w,h ->
    line_width 20
    color 0.5+0.5*sin(time), 0.5+0.5*sin(2.5*time), 0.5+0.5*sin(13.17*time), 0.75+0.25*sin(0.3*time)
    polygon
        vertex x-w/2, y-h/2
        vertex x-w/2, y+h/2
        vertex x+w/2, y+h/2
        vertex x+w/2, y-h/2
