0! -> 1
N! -> N * (N-1)!

// If-then-else statement
if true then TrueBody else FalseBody    -> do TrueBody
if false then TrueBody else FalseBody   -> do FalseBody

if true then TrueBody                   -> do TrueBody
if false then TrueBody                  -> false

// Key management
key "Control-L"                         -> toggle_full_screen
key "Escape"                            -> full_screen false
key "Control-M"                         -> toggle_hand_cursor
key "Control-1"                         -> reset_view
key "Control-+"                         -> zoom_in
key "Control--"                         -> zoom_out
key "Delete"                            -> delete_selection "Delete"
key "Backspace"                         -> delete_selection "Backspace"
key X                                   -> text_edit_key X

// Zoom
zoom_in                                 -> if zoom < 4.0 then zoom (zoom+0.25)
zoom_out                                -> if zoom > 0.25 then zoom (zoom-0.25)

// Wheel events
control_modifier  -> (keyboard_modifiers and 16#04000000) <> 0
shift_modifier    -> (keyboard_modifiers and 16#02000000) <> 0
alt_modifier      -> (keyboard_modifiers and 16#08000000) <> 0
meta_modifier     -> (keyboard_modifiers and 16#10000000) <> 0

wheel_event x, y                        ->
    if control_modifier then
        if y > 0 then
            zoom_in
        else
            zoom_out
    else
        pan -x, y

// 3D Shapes
sphere x:real, y:real, z:real, r:real   -> sphere x,y,z,2*r,2*r,2*r,25,25
sphere r:real                           ->
    sphere 0.0, 0.0, 0.0, 2*r, 2*r, 2*r, 25, 25

//2D Shapes
move_to x:real, y:real                  -> move_to x, y, 0
line_to x:real, y:real                  -> line_to x, y, 0

line x0:real, y0:real, x1:real, y1:real ->
    path
        move_to x0, y0
        line_to x1, y1

line_arrow x0:real, y0:real, s0:symbol, x1:real, y1:real, s1:symbol ->
    path
        move_to x0, y0
        line_to x1, y1
        endpoints_style s0, s1

circle x:real, y:real, r:real           -> ellipse x, y, 2*r, 2*r
circle r:real                           -> ellipse 0.0, 0.0, 2*r, 2*r
oval t                                  -> ellipse t

circular_sector x:real, y:real, r:real, a:real, b:real ->
    ellipse_arc x, y, 2*r, 2*r, a, b
circular_sector r:real, a:real, b:real  -> ellipse_arc 0.0, 0.0, 2*r,2*r, a, b

rectangle w:real, h:real                -> rectangle 0.0, 0.0, w, h

square x:real, y:real, s:real           -> rectangle x, y, s, s
square s:real                           -> rectangle 0.0, 0.0, s, s

triangle w:real, h:real                 -> triangle 0.0, 0.0, w, h
right_triangle w:real, h:real           -> right_triangle 0.0, 0.0, w, h

rounded_rectangle w:real, h:real, r:real-> rounded_rectangle 0.0, 0.0, w, h, r

polygram x:real, y:real, w:real, h:real, p:integer, q:integer ->
    star_polygon x, y, w, h, p, q
polygon x:real, y:real, w:real, h:real, p:integer ->
    star_polygon x, y, w, h, p, 1

equilateral_triangle t                  -> polygon t, constant 3
tetragon t                              -> polygon t, constant 4
diamond t                               -> polygon t, constant 4
pentagon t                              -> polygon t, constant 5
hexagon t                               -> polygon t, constant 6
heptagon t                              -> polygon t, constant 7
octagon t                               -> polygon t, constant 8
nonagon t                               -> polygon t, constant 9
decagon t                               -> polygon t, constant 10
hendecagon t                            -> polygon t, constant 11
dodecagon t                             -> polygon t, constant 12
tridecagon t                            -> polygon t, constant 13
tetradecagon t                          -> polygon t, constant 14
pentadecagon t                          -> polygon t, constant 15
hexadecagon t                           -> polygon t, constant 16
heptadecagon t                          -> polygon t, constant 17
octadecagon t                           -> polygon t, constant 18
enneadecagon t                          -> polygon t, constant 19
icosagon t                              -> polygon t, constant 20

pentagram t                             -> polygram t, constant 5, constant 2
hexagram t                              -> polygram t, constant 6, constant 2
star_of_david t                         -> polygram t, constant 6, constant 2
heptagram t                             -> polygram t, constant 7, constant 2
star_of_lakshmi t                       -> polygram t, constant 8, constant 2
octagram t                              -> polygram t, constant 8, constant 3
nonagram t                              -> polygram t, constant 9, constant 3
decagram t                              -> polygram t, constant 10, constant 3

schläfli t, {p:integer}                 -> polygon t, p
schläfli t, {p:integer/q:integer}       -> polygram t, p, q

// x..y / t -> x + (y-x) * (1 + sin(time/t * 2*pi)) / 2
x..y / t -> x

selection_color ->
    color 0.7..1.0 / 5.0, 0.8..1.0 / 9.0, 0.9..1.0 / 6.0, 0.4
    line_color 0.8, 0.6, 0.3..0.7 / 3.0, 0.8

text_selection_color ->
    color 70%,90%,100%,50%
    line_color 1,0,0,0

text_highlight_color ->
    color 70%,70%,70%,50%
    line_color 1,0,0,0

formula_highlight_color ->
    color 100%,950%,90%,20%
    line_color 1,0,0,100%

text_cursor_color ->
    color 1..0/2, 0, 0..1/2, 90%
    line_color 1,0,0,0

text_cursor_highlight ->
    color 0.6, 0.6..1.0/2, 0.6, 40%
    line_color 1,0,0,0

handle_outer_color ->
    color 0.0, 0.0, 0.0, 0.9
    line_color 0,0,0,0
handle_inner_color ->
    color 1.0, 1.0, 1.0, 1.0
    line_color 0,0,0,0

adjust_shape_handle_outer_color ->
    color 0.0, 0.0, 0.0, 0.9
    line_color 0,0,0,0
adjust_shape_handle_inner_color ->
    color 0.0, 1.0, 0.0, 0.9
    line_color 0,0,0,0

control_point_handle_outer_color ->
    color 0.0, 0.0, 0.0, 0.9
    line_color 0,0,0,0
control_point_handle_inner_color ->
    color 1.0, 0.0, 0.0, 0.9
    line_color 0,0,0,0

draw_selection x,y,z,w,h,d ->
    selection_color
    cube x-w/2, y-h/2, z-d/2, w, h, d

draw_text_selection x,y,w,h ->
    if w > 1 then
        text_selection_color
        rectangle x, y, w, h
    else
        text_cursor_color
        rectangle x, y, w, h

draw_text_highlight x,y,w,h ->
    if w > 1 then
        text_highlight_color
        rectangle x, y, w, h
    else
        text_cursor_highlight
        rectangle x, y, w, h

draw_formula_highlight x,y,w,h ->
    formula_highlight_color
    rectangle x, y, w, h

draw_3D_selection x,y,z,w,h,d ->
    selection_color
    cube x, y, z, w, h, d

draw_widget_selection x,y,w,h ->
    line_color 0,70%,90%,80%
    line_width 1
    color 0,0,0,0
    rectangle x, y, w, h

draw_selection x,y,w,h ->
    selection_color
    // svg "bubbles.svg"
    rectangle x, y, w+4, h+4

draw_selection_rectangle x, y, w, h ->
    draw_selection x,y,w,h

draw_handle x, y, z ->
    handle_outer_color
    point x, y, z, 12
    handle_inner_color
    point x, y, z, 8

draw_adjust_shape_handle x, y, z ->
    adjust_shape_handle_outer_color
    point x, y, z, 12
    adjust_shape_handle_inner_color
    point x, y, z, 8

draw_control_point_handle x, y, z ->
    control_point_handle_outer_color
    point x, y, z, 12
    control_point_handle_inner_color
    point x, y, z, 8

draw_rotation_base x, y, z ->
    selection_color
    locally
        rotatex 90
        sphere x, y, z, 50, 50, 50, 16, 8
draw_rotation_tip x, y, z -> draw_handle x, y, z

draw_translation_base x, y, z ->
    selection_color
    cube x, y, z, 50, 50, 50
draw_translation_x x, y, z -> draw_handle x + 40, y, z
draw_translation_y x, y, z -> draw_handle x, y + 40, z
draw_translation_z x, y, z -> draw_handle x, y, z + 40

draw_scale_base x, y, z ->
    selection_color
    cube x, y, z, 50, 20, 20
    cube x, y, z, 20, 50, 20
    cube x, y, z, 20, 20, 50
draw_scale_x x, y, z -> draw_handle x + 40, y, z
draw_scale_y x, y, z -> draw_handle x, y + 40, z
draw_scale_z x, y, z -> draw_handle x, y, z + 40

toolbar n:text                          -> toolbar n, ""

menu n:text                             -> menu n, "", ""
menu n:text, l:text                     -> menu n, l, ""

submenu n:text                          -> submenu n, "", ""
submenu n:text, l:text                  -> submenu n, l, ""

menu_item n:text, l:text, t             -> menu_item n, l, "", false, "false", t
menu_item n:text, l:text, i:text, t     -> menu_item n, l, i, false, "false", t
menu_item n:text, l:text, cb:boolean, ck:text, t-> menu_item n, l, "", cb, ck, t

push_button x:real, y:real, w:real, h:real, l:text, a:tree ->
      push_button x, y, w, h, l, l, a
radio_button x:real, y:real, w:real, h:real, l:text, m:text, a:tree ->
      radio_button x, y, w, h, l, l, m, a
check_box_button x:real, y:real, w:real, h:real, l:text, m:text, a:tree ->
      check_box_button  x, y, w, h, l, l, m, a

texture "default"       -> texture      ""
color "default"         -> color        0.2, 0.6, 1.0, 0.5
color "line"            -> color        0.0, 0.0, 0.0, 0.0
color "font"            -> color        0.0, 0.0, 0.0, 1.0
line_color "default"    -> line_color   0.0, 0.0, 0.0, 0.0
line_color "line"       -> line_color   0.0, 0.2, 0.8, 1.0
line_color "font"       -> line_color   0.0, 0.0, 0.0, 0.0
line_width "default"    -> line_width   4
font "default"          -> font "Times New Roman", 18

texture     "default"
color       "default"
line_color  "default"
line_width  "default"
font "default"

menu_insert -> {
menubar
menu      "menu:insert", "Insert"
submenu   "menu:insert > shape", "Shape"
menu_item "menu:insert > shape > line ---", "Line", insert
    shape
        color "line"
        line_color "line"
        line 50, -50, -50, 50
menu_item "menu:insert > shape > line -->", "Line with Arrowhead", insert
    shape
        color "line"
        line_color "line"
        line_arrow 50, -50, none, -50, 50, arrowhead
menu_item "menu:insert > shape > line <->", "Line with Two Arrowheads", insert
    shape
        color "line"
        line_color "line"
        line_arrow 50, -50, arrowhead, -50, 50, arrowhead
menu_item "menu:insert > shape > rectangle", "Rectangle", insert
    shape
        rectangle 0, 0, 100, 100
menu_item "menu:insert > shape > rounded rectangle", "Rounded Rectangle", insert
    shape
        rounded_rectangle 0, 0, 100, 100, 15
menu_item "menu:insert > shape > oval", "Oval", insert
    shape
        oval 0, 0, 100, 100
menu_item "menu:insert > shape > triangle", "Triangle", insert
    shape
        triangle 0, 0, 100, 100
menu_item "menu:insert > shape > right triangle", "Right Triangle", insert
    shape
        right_triangle 0, 0, 100, 100
menu_item "menu:insert > shape > arrow", "Arrow", insert
    shape
        arrow 0, 0, 100, 100, 44, 32%
menu_item "menu:insert > shape > double arrow", "Double Arrow", insert
    shape
        double_arrow 0, 0, 100, 100, 44, 32%
menu_item "menu:insert > shape > diamond", "Diamond", insert
    shape
        diamond 0, 0, 100, 100
menu_item "menu:insert > shape > speech ballon", "Speech Balloon", insert
    shape
        speech_balloon 0, 16, 100, 68, 50, -50, -50
menu_item "menu:insert > shape > callout", "Callout", insert
    shape
        callout 16, 0, 68, 100, 15, -50, 0, 60
menu_item "menu:insert > shape > star", "Star", insert
    shape
        star 0, 0, 100, 100, 5, 50%
menu_item "menu:insert > shape > polygon", "Polygon", insert
    shape
        polygon 0, 0, 100, 100, 5

menu      "menu:insert", ""
menu_item "menu:insert > image...", "Image...", file_chooser
    filter "All image formats (*.bmp *.gif *.jpg *.jpeg *.png *.pbm *.pgm *.ppm *.svg *.tif *.tiff *.xbm *.xpm);;" &
           "BMP - Windows Bitmap (*.bmp);;" &
           "GIF - Graphic Interchange Format (*.gif);;" &
           "JPEG - Joint Photographic Experts Group (*.jpg *.jpeg);;" &
           "PNG - Portable Network Graphics (*.png);;" &
           "SVG - Scalable Vector Graphics (*.svg);;" &
           "TIFF - Tagged Image File Format (*.tif *.tiff);;" &
           "PBM - Portable Bitmap (*.pbm);;" &
           "PGM - Portable Graymap (*.pgm);;" &
           "PPM - Portable Pixmap (*.ppm);;" &
           "XBM - X11 Bitmap (*.xbm);;" &
           "XPM - X11 Pixmap (*.xpm)"
    action insert {
    shape
        color 1,1,1,1
        image 0, 0, rel_file_path}

menu      "menu:insert", ""
submenu   "menu:insert > table", "Table..."
menu_item "menu:insert > table > 2x2", "2x2", insert
    shape
        table 2, 2, do
            table_cell_fill 
                color real table_cell_row / table_rows, real table_cell_column / table_columns, 0, 30% 
                line_color 0, 0, 1, 80% 
                line_width 1 
                rectangle table_cell_x, table_cell_y, table_cell_w, table_cell_h
            table_cell_margins 5, 5 
            table_cell
                text "Cell 1,1"
            table_cell
                text "Cell 1,2"
            table_cell
                text "Cell 2,1"
            table_cell
                text "Cell 2,2"

menu_item "menu:insert > table > 2x4", "2x4", insert
    shape
        table 2, 4, do
            table_cell_fill 
                color 0,0,0,20%
                line_color 0, 0, 1, 80% 
                line_width 1 
                rounded_rectangle table_cell_x, table_cell_y, table_cell_w, table_cell_h, 10
            table_cell_margins 5, 5 
            table_cell
                text "Cell 1,1"
            table_cell
                text "Cell 1,2"
            table_cell
                text "Cell 2,1"
            table_cell
                text "Cell 2,2"
            table_cell
                text "Cell 3,1"
            table_cell
                text "Cell 3,2"
            table_cell
                text "Cell 4,1"
            table_cell
                text "Cell 4,2"

menu_item "menu:insert > table > 3x2", "3x2", insert
    shape
        table 2, 4, do
            table_cell_fill 
                color 0,0,0,20%
                line_color 0, 0, 1, 80% 
                line_width 1 
                rectangle table_cell_x, table_cell_y, table_cell_w, table_cell_h, 10
            table_cell_margins 5, 5 
            table_cell
                text "Cell 1,1"
            table_cell
                text "Cell 1,2"
            table_cell
                text "Cell 1,3"
            table_cell
                text "Cell 2,1"
            table_cell
                text "Cell 2,2"
            table_cell
                text "Cell 2,3"
}


toolbar_insert -> {
toolbar   "toolbar:insert", "Insert", "N"
submenu   "toolbar:insert > shape", "Shape", "images/tb_shapes.png"
menu_item "toolbar:insert > shape > line ---", "", "images/tb_shape_line.png", insert
    shape
        color "line"
        line_color "line"
        line 50, -50, -50, 50
menu_item "toolbar:insert > shape > line -->", "", "images/tb_shape_line_w_arrowhead.png", insert
    shape
        color "line"
        line_color "line"
        line_arrow 50, -50, none, -50, 50, arrowhead
menu_item "toolbar:insert > shape > line <->", "", "images/tb_shape_line_w_two_arrowheads.png", insert
    shape
        color "line"
        line_color "line"
        line_arrow 50, -50, arrowhead, -50, 50, arrowhead
menu_item "toolbar:insert > shape > rectangle", "", "images/tb_shape_rectangle.png", insert
    shape
        rectangle 0, 0, 100, 100
menu_item "toolbar:insert > shape > rounded rectangle", "", "images/tb_shape_rounded_rectangle.png", insert
    shape
        rounded_rectangle 0, 0, 100, 100, 15
menu_item "toolbar:insert > shape > oval", "", "images/tb_shape_oval.png", insert
    shape
        oval 0, 0, 100, 100
menu_item "toolbar:insert > shape > triangle", "", "images/tb_shape_triangle.png", insert
    shape
        triangle 0, 0, 100, 100
menu_item "toolbar:insert > shape > right triangle", "", "images/tb_shape_right_triangle.png", insert
    shape
        right_triangle 0, 0, 100, 100
menu_item "toolbar:insert > shape > arrow", "", "images/tb_shape_arrow.png", insert
    shape
        arrow 0, 0, 100, 100, 44, 32%
menu_item "toolbar:insert > shape > double arrow", "", "images/tb_shape_double_arrow.png", insert
    shape
        double_arrow 0, 0, 100, 100, 44, 32%
menu_item "toolbar:insert > shape > diamond", "", "images/tb_shape_diamond.png", insert
    shape
        diamond 0, 0, 100, 100
menu_item "toolbar:insert > shape > speech ballon", "", "images/tb_shape_speech_balloon.png", insert
    shape
        speech_balloon 0, 16, 100, 68, 50, -50, -50
menu_item "toolbar:insert > shape > callout", "", "images/tb_shape_callout.png", insert
    shape
        callout 16, 0, 68, 100, 15, -50, 0, 60
menu_item "toolbar:insert > shape > star", "", "images/tb_shape_star.png", insert
    shape
        star 0, 0, 100, 100, 5, 50%
menu_item "toolbar:insert > shape > polygon", "", "images/tb_shape_polygon.png", insert
    shape
        polygon 0, 0, 100, 100, 5
}

menu_format -> {
menubar
menu   "menu:select:format", "Format"
menu_item "menu:select:format > fill color", "Fill Color...", do
    color_chooser "color", do
        set_attribute "color", {color red, green, blue, alpha}
menu_item "menu:select:format > line color", "Line Color...", do
    color_chooser "line_color", do
        set_attribute "line_color", {line_color red, green, blue, alpha}
menu_item "menu:select:format > font", "Font...", do
    font_chooser
        set_attribute "font", {font font_family, font_size, slant font_slant%, weight font_weight%, stretch font_stretch%}

menu   "menu:select:arrange", "Arrange"
menu_item "menu:select:arrange > toFront", "Bring to front", do
    bring_to_front
menu_item "menu:select:arrange > forward", "Bring forward", do
    bring_forward
menu_item "menu:select:arrange > backward", "Send backward", do
    send_backward
menu_item "menu:select:arrange > toBack", "Send to back", do
    send_to_back
menu_item "menu:select:arrange > group", "Group", do
    group_selection
menu_item "menu:select:arrange > ungroup", "Ungroup", do
    ungroup_selection
}


menu_insert
menu_format
